#__PrivCrochet_version:1
__PrivCrochet_colorID: 0
__PrivCrochet_position: -200,40
__PrivCrochet_tags: 
title: 1
---
You enter the club room for the first time.
The words "self-portrait" are projected onto the screen.
You should probably explore what and who is in the club room first.
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 40,40
__PrivCrochet_tags: 
title: 2_1
---
<<if visited("2_1") > 1>>
    Vera: Hey again, {$name}.
<<else>>
    You: "Hi! I'm {$name}."
    Vera: "Hi, {$name}, I'm Vera."
    <<if $name == "Charlotte">>
        Vera: "Wow, you look great!"
        You: "Thanks!" (+5% confidence)
        <<changeConfidence(5)>>
    <<endif>>
<<endif>>

<<jump 2_1_options>>
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 1000,120
__PrivCrochet_tags: 
title: 5
---
There's a few pieces of paper and a box of colour pencils before you.
Before you can ask what you should do with them, a girl you've never seen before enters the room.
New Girl: "Hi! Wait. I don't think I've seen you before..."
You: "Hi, I'm {$name}."
New Girl: "Hello! I'm Eleanor. I kind of keep this club going, I guess."
Eleanor: "Today's theme is quite simple: self-portrait."

-> You: "Isn't that quite hard?"
    Eleanor: "Nah, don't doubt yourself!"
    Eleanor: "We all have to start somewhere."
    Eleanor: "Don't worry about whether your work is good, just get it out there." (+5% confidence) <<changeConfidence(5)>>
    You: "Thank you for that."
    Eleanor: "No problem! We'll share our art in 20 minutes."

-> You: "That sounds simple enough."
    Eleanor: "Awesome! We'll share our art in 20 minutes."
    Eleanor: "Can't wait to see what you'll make!"
<<jump 6>>
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 1000,360
__PrivCrochet_tags: 
title: 6
---
You take out your phone for a reference image. Is it going to be your male, or female self?

-> Male
    <<set $drawing = "Male">>
-> Female
    <<set $drawing = "Female">>

You start drawing. Before you know it, 20 minutes have passed.
<<jump 7>>
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 1240,360
__PrivCrochet_tags: 
title: 7
---
Eleanor: "Wow, {$name}! That's a nice drawing."
Eleanor: "Wanna show it to the rest of us?"

-> "Okay, sure."
    Eleanor: "Alright, everyone, {$name} has something for us to see!"
    <<if $name == "Charlotte">>
        <<if $drawing == "Female">>
            <<jump 10>>
        <<else>>
            <<jump 15>>
        <<endif>>
    <<else>> //male 
        <<if $drawing == "Female">>
            <<jump 13>>
        <<else>>
            <<jump 18>>
        <<endif>>
    <<endif>>

-> "No thanks."
    Eleanor: "You sure? I mean, with you being new here, it'd be nice to introduce yourself..."

    -> "No and stop asking."
        Eleanor: "Okay, whatever you're comfortable with."
        The rest of the session passes uneventfully. Awkward...
        <<jump end>>
    
    -> "Okay, fine."
        Eleanor: "Don't worry about it, we won't judge."
        <<if $name == "Charlotte">>
            <<if $drawing == "Female">>
                <<jump 10>>
            <<else>>
                <<jump 15>>
            <<endif>>
        <<else>>
            <<if $drawing == "Female">>
                <<jump 13>>
            <<else>>
                <<jump 18>>
            <<endif>>
        <<endif>>
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 280,40
__PrivCrochet_tags: 
title: 2_2
---
You: "Hi! I'm {$name}."
Anthony: "Hi, I'm Anthony. Please don't disturb me."
Anthony seems very engrossed in his art. Let him be.

<<jump 2_2_options>>
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 1560,-40
__PrivCrochet_tags: 
title: 10
---
You: "Hi, I'm Charlotte, and this drawing is me. I tried my best..."
Nadia: "Ooh. I can see some resemblance, but... I'm not sure if I should say it..."
You: "What is it?"
Nadia: "Well, I may not be the best at drawing female bodies."
Nadia: "But your drawing seems a little more... rectangular? Like, it's not curvy."

-> You: "I'm just built different." #skillcheck #cha # 6
    <<set $diceroll = diceRoll($cha)>>
    [You rolled a {$diceroll}.]
    <<if $diceroll < 6>>
        Damn, that wasn't good.    
        <<changeConfidence(-5)>>    
    <<endif>>
    Anthony: "Yeah, can tell. Your voice is as low as mine - "
    <<addExperience(10)>>
    Vera: "Hey. Every woman has a different body shape. Not everyone's got an hourglass figure, or a high voice." (+10 XP)
    Nadia: "Yeah, I suppose..."
    Anthony: "Then what about you, Vera? How come you taller than me even though you're a girl?"
    Eleanor: "Anthony, enough. We're not here to comment on appearances. Only art."
    Anthony: "Fine. But the art still doesn't look womanly enough."
    Nadia: "Charlotte, I'm so sorry..."

    -> You: "It's fine, you were coming from an artistic perspective."
        Nadia: "Yeah, that was my only intent. I didn't want to offend you personally."
        Nadia: "I can recommend some websites where I learned to draw, maybe they would help?" (+ 5 XP) 
        <<addExperience(5)>>
        Anthony smirks.
        Eleanor: "Thanks Nadia. Please remember, we're just here to create."
        Eleanor: "Oh dear, look at the time, we're almost done!"
    
    -> You: "Just stop."
        Nadia: "Okay..."
        Anthony: "Wow, you can't even take feedback."
        Vera: "Charlotte's feelings are valid."
        Anthony: "Sure, whatever. Men in dresses defending each other." (-5% Anthony relationship) 
        <<changeRelationship("A", -5)>>
        Eleanor: "Anthony, stop!"
        Eleanor: "Sorry, everyone, we're out of time..."

-> You: "You're right, my drawing isn't that good..."
    Nadia: "Oh I'm so sorry!"
    Anthony: "Nah, she's got a point. Females don't look like that." (-5% confidence) 
    <<changeConfidence(-5)>>
    Vera: "Hey, let Charlotte draw however she wants to."
    Anthony: "What? I'm just telling the truth. How is he supposed to improve if he can't even take a few knocks?"
    Anthony: "Or is he just trying to get pity from us?"
    Nadia: "I guess so..."
    
    -> You: "This isn't just a drawing issue, isn't it?"
        Anthony: "What are you talking about?"
        You: "You hate the way I dress."
        Anthony: "Don't put words in my mouth."
        Vera: "But you used 'he/him' for Charlotte - "
        Anthony: "Ya what. His voice so deep - "
        Eleanor: "Okay guys, enough! Charlotte, I'm sorry you had to hear all that."
        Eleanor: "Anyway, our time's almost up, so..."
    
    -> You: "Forget it. My drawing sucks anyway."
        Eleanor: "Come on, Charlotte..."
        Anthony: "How about learning what people look like first? That way your art won't look so cui." (-5% confidence) 
        <<changeConfidence(-5)>>
        Eleanor: "That's enough, Anthony."
        Nadia: "Charlotte, there's some videos I can recommend for drawing. Maybe you wanna watch those to start?"
        You: "Thanks, Nadia."
        Eleanor: "Yeah... looks like our time's also about up."

-> You: "Thanks for the feedback, I guess."
    Nadia: "Yeah, maybe you can cinch the waist a little? That would bring out the figure a bit."
    Vera: "Isn't she wearing a rather straight dress, though?"
    Anthony: "Yeah, it really makes you wonder what's under it."
    Eleanor: "Anthony!"

    -> You: "I don't think I'll change it, though."
        Nadia: "Oh?"
        You: "Yeah. Art is up to us to create. If my body looks more rigid than a normal girl's, so be it."
        Vera: "Love that for you! Be proud of your body." (+5% confidence) 
        <<changeConfidence(5)>>
        Nadia: "Of course. We're here for you, Charlotte."
        Anthony rolls his eyes.
        Eleanor: "Okay looks like we're all out of time!"
    
    -> You: "I'll see what I can do to change that."
        Nadia: "Up to you. We all draw what we like to see."
        Vera: "Yeah, even my drawing is quite stylised. Doesn't need to be accurate."
        Anthony: "Of course. Look at all the rainbows..."
        Vera: "You got a problem with that, Anthony?"
        Eleanor: "Okay looks like we're all out of time!"

    -> You: "The truth is that I'm Charlotte." #skillcheck #cha # 6
        <<set $diceroll = diceRoll($cha)>>
        [You rolled a {$diceroll}.]
        <<if $diceroll < 6>>
            You say that, but do you really feel it? (-5% confidence) 
            <<changeConfidence(-5)>>
            Vera: "Love that for you! Charlotte is who you really are."
            Nice to hear, but are you sure she's genuine?
        <<else>>
            Yes, that's a good start.
            Vera: "Love that for you! Charlotte is who you really are."
            <<changeConfidence(5)>>
            Aww, Vera... you're so nice. (+5% confidence)
        <<endif>>
        
        Nadia: "You have our support."
        Anthony is looking at his phone, clearly eager for the session to end.
        Nadia: "You know, I'm trying to get better at drawing female bodies too." 
        Nadia: "Here's some videos that I'm procrastinating on watching. Hope they help you!"
        Eleanor: "Thanks Nadia. Anyway, our time is almost up, so..."

<<jump 11>> // They all go to 11.
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 1840,-40
__PrivCrochet_tags: 
title: 11
---
Eleanor: "Thanks for coming and see you next week!"

<<goto_room_cca_1_seated_11()>>
As Nadia and Anthony leave, Vera stops you for a quick chat.
Vera: "Hey Charlotte. Are you feeling okay?"

-> You: "Yeah, I'm still fine."
    Vera: "That's good. "
    Vera: "Still, it doesn't excuse Anthony from being an asshole back there."

-> You: "Uh... not really..."
    Vera: "Oh dear..."
    Vera: "Some people are just terrible. Anthony's one of them, I guess."
    Vera: "You should take the night off. And the next few days, if you can. Text me if you need, like, emotional support."
    You: "Thanks Vera... I appreciate the help." (+5% confidence)  
    <<changeConfidence(5)>>

<<jump 12>>
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 2080,-40
__PrivCrochet_tags: 
title: 12
---
Vera: "People like us shouldn't be living like this. And that's why I want to do something about it."
You: "Wait... people like us?"
Vera: "You know, trans women."

-> You: "Uh, I'm not sure I identify with that..."
    Vera: "Oh?"
    You: "Yeah, don't I need to do, like, hormones and surgery before I reach there?"
    Vera: "Well, that's what some of us would do."
    <<addExperience(10)>>
    Vera: "But it's not compulsory. You are Charlotte as long as you want to be Charlotte." (+ 10 XP)
    
    -> You: "Are you saying that just to be nice?"
        Vera: "Absolutely not."
        Vera: "Your body, your opinion. Don't strictly follow what anyone else says."
        Vera: "This may be our first meeting, but already I see a lot of my early life in what you're going through."
        Vera: "But I don't think I can tell you everything now."
        Vera: "Let's meet up soon?"
        You: "Sure, I'll let you know."
    
    -> You: "Even if I identify as male sometimes?"
        Vera: "That's totally valid. After all, it's your choice."
        Vera: "Or maybe it's not. Maybe you have to perform the male gender for school, or home, or any other societal reason."
        You: "Yeah, I think that's how I'd put it."
        Vera: "Mm. Just know that you shouldn't feel shame for it."
        Vera: "Anyway, I don't think I can tell you everything now."
        Vera: "Let's meet up soon?"
        You: "Sure, I'll let you know."
    
    -> You: "Thank you for that."
        Vera: "Yeah, I think you needed to hear that after just now."
        Vera: "Anyway, I don't think I can tell you everything now."
        Vera: "Let's meet up soon?"
        You: "Sure, I'll let you know."
    
    <<jump end>>

-> You: "Right."
    Vera: "We've got to stand up for each other."
    Vera: "And I'm guessing you've only just started your journey?"
    You: "It's not my first time crossdressing."
    Vera: "Oh, so no HRT?"
    You: "What's that?"
    Vera: "Hormones. Estrogen. You don't know anything about it?"

    -> You: "I just know it's the female hormone."
        Vera: "Yeah. So I'm guessing you haven't started that?"
        You: "Nope."
        Vera: "That's valid. It's a big step to take."
        Vera: "I can't tell you all about it now, but we could meet to talk about it soon?"
        You: "Sure, I'll let you know."
    
    -> You: "No..."
        Vera: "Woah. You've never Googled this?"
        Vera: "Basically there's a few hormones that can turn your body into the one that you want."
        Vera: "I can't tell you all about it now, but we could meet to talk about it soon."
        You: "Sure, I'll let you know. Thanks!"
    
    <<jump>>

-> You: "So what do you want to do about it?"
    Vera: "Advocacy. Petitions. Civil unrest, question mark."
    <<addExperience(10)>>
    Vera: "But even surviving this far, is my personal form of resistance against a system that won't accept me for who I am." (+ 10 XP) 

    -> You: "I'm proud of you for that." #skillcheck #emp # 6
        <<set $diceroll = diceRoll($emp)>>
        [You rolled a {$diceroll}.]
        <<if $diceroll < 6>>        
            Vera: "Thanks."
        <<else>>
            <<changeRelationship("V", 5)>>
            Vera: "Thank you, dear. Being a girl is tough." (+5% Vera relationship) 
        <<endif>>

        Vera: "There's lots of stuff I have to do daily, but it's worth it to wake up in the right body every morning."
        You: "Like what?"
        Vera: "I can't tell you all about it now, but we could meet to talk about it soon."
        You: "Sure, I'll let you know. Thanks!"
    
    -> You: "I'm not sure I got all of that."
        Vera: "Ahh, I'm sorry... I tend to overshare when I get excited."
        Vera: "I can't tell you all about it now, but we could meet to talk about it soon."
        You: "Sure, I'll let you know. Thanks!"
        
    -> You: "Wait, civil unrest?"
        Vera: "Relax, I'm not doing anything illegal."
        Vera: "After all, it's already tough enough to get by, I don't need legal troubles on top of that."
        You: "I'm sorry..."
        Vera: "It's okay." 
        Vera: "Anyway, I don't think I can tell you everything now."
        Vera: "Let's meet up soon?"
        You: "Sure, I'll let you know." 

<<jump end>>
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 1560,200
__PrivCrochet_tags: 
title: 13
---
You: "Hi, I'm Charles, but I also go by Charlotte when I feel female, so here's what I drew..."
Anthony: "Wait... why?"
You: "Why what?"
Anthony: "Why do you feel female?"

-> You: "Don't you ever wonder what it's like to be the other gender?"
    Anthony: "Sounds kinda sus to me."
    Vera: "Well, you're just missing out then. I'm sure Charlotte is living her best life."
    Anthony: "You're just delusional. How can a man look pretty in a dress?"
    Nadia: "I don't know, I've seen some anime guys in maid dresses - "
    Anthony: "Seriously? Those are all just jokes and fan art."

    -> You: "You should try it. After all, I have." 
        Vera: "So that's the real inspiration for Charlotte."
        Anthony: "Oh my god, that's so weird."
        Eleanor: "That's enough, Anthony."
    
    -> You: "Yeah, just jokes."
        Anthony: "Right? It's not like you actually are deluded enough to think you can look like a girl."
        Anthony: "I've seen them before, they're all super tall and their faces are super square - "
        Eleanor: "That's enough, Anthony."

-> You: "I'm genderqueer."
    Anthony: "What the hell is that even? Some alphabet mafia shit?" 
    Vera: "You got a problem with queer people?"
    Nadia: "Guys... please... chill out..."
    Eleanor: "Okay, everyone, enough."
    Anthony: "See la, so agitated for what?"
    Vera: "You'd better respect everyone's identity here."
    Anthony: "Who made you the boss?"
    Eleanor: "No, Anthony, she's right. You started this."
    Anthony: "Fine, I'll just shut up then."

-> You: "Cannot ah?" #skillcheck #cha # 6
    <<set $diceroll = diceRoll($cha)>>
    [You rolled a {$diceroll}.]
    <<if $diceroll >= 6>>
        You felt confident saying that. Anthony certainly doesn't, upon hearing it. (+5% confidence) 
        <<changeConfidence(5)>>
    <<endif>>
    Anthony: "Does no one else feel weirded out by this?"
    Vera: "What's so weird about it? Charlotte is a woman if she wants to be."
    Nadia: "You look good, Charlotte."
    Anthony: "Fine. The whole goddamn world is upside down anyway."
    Vera: "You are Charlotte if you want to be. Don't let anyone tell you otherwise."
    Eleanor: "That's enough, Anthony."

-> You: "I don't know... I just feel like it."
    Anthony: "Wow. Your drawing sure doesn't show it."
    Nadia: "Come on, he had 20 minutes. That's barely enough time."
    Anthony: "Then stick to drawing a male. You can see that in the mirror already."
    Vera: "I think Charlotte's drawing is good!"
    Anthony: "Don't be delusional. It's out of proportion."
    Anthony: "And come to think of it, your proportions are also kinda weird, aren't they, Vera?"
    Eleanor: "Enough! Anthony, that's going too far."
    Anthony: "Fine. I stand by what I said."
    Vera: "Charlotte, don't listen to him. You're valid."

<<jump 14>> //All go to 14.
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 1840,200
__PrivCrochet_tags: 
title: 14
---
Eleanor: "We didn't even get to comment on Charlotte's art... if you want to talk about it, I can have a look later."
Eleanor: "Anyway, our time's up for this week. See you next week!"
<<jump end>>
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 1560,440
__PrivCrochet_tags: 
title: 15
---
You: "Hi, I'm Charlotte, but this is how I usually look like."
Anthony: "So you're actually a guy."
You're really being put on the spot here. How will you respond?

-> You: "Yeah..."
    Anthony: "Is this some kind of fetish?"
    Eleanor: "Anthony, please."
    <<changeRelationship("A", -5)>>
    <<changeConfidence(-5)>>
    Anthony: "Men don't wear dresses." (-5% Anthony relationship; -5% confidence) 

    -> You: "Why not?"
        Anthony: "What, did you not get taught this when you were a child?"
        <<addExperience(10)>>
        Vera: "Let people wear what they want. It's not your business." (+10 XP) 
        Anthony: "Neither is it yours."
        Vera: "It's my business if you're offending people - "
        Eleanor: "Thank you, Vera, that's quite enough."
    
    -> You: "Yeah, I guess..."
        <<changeConfidence(-5)>>
        Anthony: "You're seriously messed up in the head. You need mental help." (-5% confidence) 
        Vera: "You're saying that as if mental health support is a bad thing."
        Anthony: "It's not. Whatever the hell he's doing is, though."
        Vera: "Oh, you have a problem with queer people?"
        Eleanor: "Vera, enough! Anthony, you too."

-> You: "But what is a guy?" #skillcheck #int # 6
    <<set $diceroll = diceRoll($int)>>
    [You rolled a {$diceroll}.]
    Anthony: "What?"
    
    <<if $diceroll >= 5>>
        You: "That definition always changes, right? It's not as easy as just 'what's down there'."
        You: "I'm a crossdresser, so I go between guy and girl. It's that easy." (+10 XP) 
        <<addExperience(10)>>
        Vera: "Love that for you! Charles and Charlotte."
        Anthony: "Does anyone not see how weird this is?"
        He is met with silence.
        Anthony: "Guess I'm the only one with any common sense left."
    <<else>>
        You: "Yeah. Why can't a guy wear dresses and makeup too?"
        Anthony: "I'm not gonna sugarcoat it. That's really weird."
        Anthony: "How many guys do you see do that? I certainly don't."
        Vera: "Hey, let Charlotte wear whatever she wants."
        <<changeConfidence(-5)>>
        Anthony: "Why are you referring to him by that name? His name is Charles. Let's not feed into delusions here." (-5% confidence) 
    <<endif>>

-> You: "Anything wrong with that?"
    Anthony: "Does no one else feel weirded out by this?"
    Vera: "What's so weird about it? Charlotte is a woman if she wants to be."
    Nadia: "You look good, Charlotte."
    Anthony: "Fine. The whole goddamn world is upside down anyway."

<<jump 16>>
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 1840,440
__PrivCrochet_tags: 
title: 16
---
Vera: "You are Charlotte if you want to be. Don't let anyone tell you otherwise."
Vera: "I am curious, though. Why not draw Charlotte?"

-> You: "Drawing girls is hard."
    Nadia: "Big mood. It's so weird, I like guys, but I have this..." 
    Nadia: "I guess you could call it pressure on myself to draw girls well?"
    Vera: "Yeah. Doesn't help that a lot of reference models for girls don't resemble our bodies."
    Anthony: "Ya, like yours. You're so damn tall. And you, your voice is so low - "
    Vera: "You know, Anthony, maybe you're insecure about your own height, since you like to point out mine so much."
    Eleanor: "That's enough! Both of you."

-> You: "I'm not Charlotte all the time."
    Vera: "Oh?"
    You: "Yeah... I still need to maintain my responsibilities as a guy."
    You: "I've seen what it's like for people to transition and... well, I'm still scared of that."
    Vera: "Well, I guess that's fair. But..."
    Eleanor: "I hate to stop this discussion, but... our room booking is about to end."
    Eleanor: "Maybe we can continue this outside?"

<<jump 17>>
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 2080,440
__PrivCrochet_tags: 
title: 17
---
Eleanor: "We didn't even get to comment on Charlotte's art... if you want to talk about it, I can have a look."
Eleanor: "Either way, see you next week!"
<<jump end>>
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 1581,682
__PrivCrochet_tags: 
title: 18
---
You: "Hi, I'm Charles, and this is me. I tried my best..."
Vera: "Well, that's not a bad attempt."
Nadia: "Do you want an anime styled drawing? I can try that!"
Anthony: "So, just an average dude."
Eleanor: "For something drawn in 20 minutes, that's pretty good!"
Eleanor: "Come for more sessions and you'll learn more."
Eleanor looks at the clock.
Eleanor: "Well, that's everyone." 
Eleanor: "We've still got a bit of time, and I brought snacks, so please help yourselves!" 
Eleanor: "Get to know each other, yeah?"

<<goto_room_cca_1_seated_18()>>

Vera and Nadia leave first, Anthony waits for them to go before he comes over to you.
Anthony: "Hey. Didn't you realise something was... off about that girl?"

-> You: "Like what?"

-> You: "Yeah, she doesn't look normal."
    <<changeRelationship("A", 5)>>
    Anthony: "Right? Anyone can see it from a mile away." (+5% Anthony relationship) 
    
<<jump 19>>
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 1819,680
__PrivCrochet_tags: 
title: 19
---
Anthony: "She's taller than both of us, her voice is lower than other girls'..."
Anthony: "Maybe she's an ah gua."

-> You: "Oh my goodness, that's so disgusting."
    Anthony: "Right? It's all this gender and feminism bullshit that's going around."
    You: "Yes, it's very confusing."
    Anthony: "It's also the reason why I don't have a girlfriend."
    Anthony: "Tell you ah, there's this influencer that I watch, he says women are too liberal and free already."
    <<addExperience(5)>>
    Anthony: "Here's the link. I think it's very helpful. Especially if you don't have a girlfriend also." (+ 5 XP) 
    You: "Thanks, I'll take a look."
    <<changeRelationship("A", 5)>>
    Anthony: "You'll see the truth. See you around." (+5% Anthony relationship) 

-> You: "That's not very nice..."
    Anthony: "I'm just saying it as it is. Females don't look like that."
    Anthony: "I didn't want to say it during the session, later Eleanor get angry at me."
    Anthony: "But you agree, right? This Vera is not normal."

    -> You: "Yes."
        Anthony: "The world has really turned upside down."\
        <<addExperience(5)>>
        Anthony: "Here, watch this influencer when you go back." (+ 5 XP) 
        Anthony: "I think he is very helpful in understanding what to do in this fucked up world."
        You: "Thanks."
        <<changeRelationship("A", 5)>>
        Anthony: "See you next week." (+5% Anthony relationship) 
    
    -> You: "I mean, she has every right to exist."
        Anthony: "You must have heard too much woke content. Men can be women, there are 76 genders, all that bullshit."
        Anthony: "You need to break out of the filter bubble."
        <<addExperience(5)>>
        Anthony: "Here, watch this influencer when you go back." (+ 5 XP) 
        Anthony: "I think he is very helpful in understanding what to do in this fucked up world."

        -> You: "Okay, I'll give it a shot."
            Anthony: "He helped me. I hope he helps you too."

        -> You: "Wait, isn't this just incel content?"
            Anthony: "Yes. I'm an incel."
            Anthony: "I don't say it out loud, but I think you'd understand."
            Anthony: "It's not safe out there with all the woke opinions. The only truth is in people like this influencer."
            Anthony: "We should leave, the room's going to close. See you around."

-> You: "She's beautiful in her own way."
    Anthony: "That is the most cliche thing I have ever heard."

    -> You: "I mean, people are sometimes just built different."
        Anthony: "Yeah, not so different that they become freaks."
        <<addExperience(10)>>
        You: "Let me finish. What about you? Ever heard people say you're too short before?" (+ 10 XP) 
        Anthony: "All the time. It's not something I can change."

        -> You: "That didn't feel very good, did it?" #skillcheck #emp # 6
            <<set $diceroll = diceRoll($emp)>>
            [You rolled a {$diceroll}.]
            <<if $diceroll >= 6>>
                Anthony: "Of course. I feel like shit every time I think of those stupid femoids who don't want to date me."
                You: "Then if you don't want people to say you short, why you say Vera tall and all that?"
                Anthony: "I just - I just..."
                <<addExperience(10)>>
                You: "You gotta respect other people if you want respect in return." (+ 10 XP) 
                You: "Anyway, room's closing already. Time to go."
            <<else>>
                Anthony: "What to do? The world sucks."
                Anthony: "Should just lie down and rot."
                You: "If that's what you want, I guess."
                You realise you can't really continue from here. It's time to leave.
            <<endif>>

        -> You: "But it's just what people say, isn't it? Do you listen to everything other people say?" #skilcheck #int # 6
            Anthony: "But it's proven what. Females aren't attracted to you if you're not 6-6-6."
            <<set $diceroll = diceRoll($int)>>
            [You rolled a {$diceroll}.]
            <<if $diceroll >= 6>>
                You: "Then what about all the men below 6 feet who are married?"
                Anthony: "They... they're the lucky few! Or they're rich! Or something!"
                You: "Yep, you don't have an answer."
                You: "I'm sure there are guys out there who aren't 6-6-6. And they're still happy."
                You: "And Vera is still happy, too. Regardless of what you say about her."
                <<addExperience(10)>>
                You: "So why don't you look within yourself and find out why you're unhappy?" (+10 XP) 
                You: "Maybe that'll change what you say about people around us."
                You walk off. That was... strangely liberating!
            <<else>>
                Anthony: "I mean, do you have a girlfriend?"
                You: "No..."
                Anthony: "You don't understand how hard it is to be a man like me."
                Anthony: "No girlfriend, not valued for being myself."
                You: "Well, that's tough."
                Anthony: "Where were we? Oh right, Vera."
                Anthony: "She's weird. That's all. See you next week."
                You're back to square one...
            <<endif>>

    -> You: "Haven't you ever seen any transgender people before?"
        Anthony: "Oh, so that's what they look like. Men in dresses, trying to be women."
        Anthony: "It's disgusting."

        -> You: "Yeah, why don't you say that in front of Vera and Eleanor. I'm sure they'll absolutely love it." #skillcheck #cha # 6
            <<set $diceroll = diceRoll($cha)>>
            [You rolled a {$diceroll}.]
            <<if $diceroll >= 6>>
                Anthony wants to say something, but can't bring himself to say it.
                <<addExperience(10)>>
                You: "That's what I thought." (+10 XP) 
                It's time to leave.
            <<else>>
                Anthony: "Why should I care that they want to be delusional and PC?"
                Anthony: "You should let go of being woke also. Before it's too late."
                You: "Yeah, we're done here. Time to leave."
            <<endif>>
            
        -> You: "We're done here."
            You: "I'm not comfortable with how you're speaking about them."
            <<changeRelationship("A", -5)>>
            Anthony: "So be it." (-5% Anthony relationship) 

    -> You: "I mean, I dress up sometimes, too." #skillcheck #cha # 6
        <<set $diceroll = diceRoll($cha)>>
        [You rolled a {$diceroll}.]
        Anthony: "Wow. Another one." (-5% Anthony relationship) <<changeRelationship("A", -5)>>
        Anthony: "You all are delusional."
        <<if $diceroll >= 6>>            
            <<changeConfidence(5)>>
            You: "I'm proud of who I am." (+5% confidence) 
            Anthony: "Proud of being a freak."
            Anthony: "Proud of denying that you are a man."
            Anthony: "There's nothing more pathetic and weird than that."
            You: "Keep talking. I'm gonna leave."
        <<else>>
            <<changeConfidence(-5)>>
            You: "Don't say that..." (-5% confidence) 
            Anthony: "I didn't know this Art Club was such a woke gender circus."
            Anthony: "I'm sure there will be many more opportunities to laugh at how cringe the two of you are."
            Anthony: "See you next week."
        <<endif>>
<<jump end>>
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 1000,-120
__PrivCrochet_tags: 
title: pre-5
---
There's one empty seat at the table.
Do you wish to sit?

-> No (go back)

-> Yes (continue...)
    <<goto_room_cca_1_seated()>>
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 2520,240
__PrivCrochet_tags: 
title: end
---
You head back to your room.

<<goto_room_bedroom_after_cca()>>
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 40,280
__PrivCrochet_tags: 
title: 2_1_options
---
What do you say?

-> "How are things here?" <<if !optionChosen()>>
    Vera: "Are you new? I've never seen you before."
    Vera: "Well, we usually have a prompt for the week which we try to draw out."
    Vera: "Then we can walk around and see each others' progress."
    <<jump 2_1_options>>

-> "What are you drawing?"
    Vera reveals her drawing. It's her face on a very psychedelic background.
    Vera: "Yes, I know, lots of rainbows."
    
    -> "Did it take you long to make this?" <<if !optionChosen()>>
        Vera: "Haha, I guess not... I had the idea in mind already."
        Vera: "But it's more complex than just spreading colour over paper."
        <<jump 2_1_options>>
    
    -> "What do the rainbows mean?" <<if !optionChosen()>> #skillcheck #int # 5
        Vera: "Take a guess."
        <<set $diceroll = diceRoll($int)>>
        [You rolled a {$diceroll}.]
        <<if $diceroll >= 5>>
            <<addExperience(10)>>
            You: "It's an LGBT thing." (+10 XP)
            Vera: "Yes, absolutely. I'm a queer woman."
            You: "Oh, that's cool."
            <<changeRelationship("V", 5)>>
            Vera: "Thank you!" (+5% Vera relationship)
            Vera: "I'm guessing you'd know a thing or two about that too." <<if $name == "Charlotte">>
        <<else>>
            You: "Uh... is it a weather thing? Or some art movement I've never heard of?"
            Vera: "Haha, neither of those."
            Vera: "Don't worry, I'm not offended. Though I am a bit surprised you don't know."
            You: "Don't know what?"
            Vera: "Rainbows? LGBT? You've never seen the pride flag before?"
            You: "Uh..."
            Vera: "But yeah, that's what it means. I'm queer and proud."
        <<endif>>
        <<jump 2_1_options>>
    
    -> "It looks nice!" <<if !optionChosen()>>
        <<changeRelationship("V", 5)>>
        Vera: "Thank you~ You like rainbows too?" (+5% Vera relationship)
        You: "I don't see a lot of them around. But yours looks good."
        Vera: "Yes, the world could do with a lot more colour."
        Vera: "I guess that's what I want to say through my art, too."
        Vera: "You should start your drawing! Don't wanna hold you up any longer."
        // end Vera interaction

    -> "It looks excessive." <<if !optionChosen()>>
        <<changeRelationship("V", -5)>>
        Vera: "Excuse me?"
        You: "Yeah. Too many rainbows. And you didn't finish your face."
        Vera: "Okay, fine. It's just a work in progress."
        Vera: "But damn, I didn't know you were the sort to take offence at rainbows." (-5% Vera relationship)
        // end Vera interaction

    -> "Alright, I'll leave you be then."
        Vera: "See you."
        // end interaction

-> "Alright, I'll leave you be then."
    Vera: "See you."
    // end interaction
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 280,280
__PrivCrochet_tags: 
title: 2_2_options
---
What do you say?

-> "How are things here?" <<if !optionChosen()>>
    Anthony dismissively points to the whiteboard.
    Anthony: "Draw something based on the prompt. We usually talk about it later."
    <<jump 2_2_options>>

-> "What are you drawing?"
    Anthony shows you a picture of an anthill, with ants all around it.
    Anthony: "Ant-thony. Get it?"
    <<jump 2_2_options2>>

-> "Alright, I'll leave you be then."
    Anthony: "Mm."
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 520,40
__PrivCrochet_tags: 
title: 2_3
---
<<if visited("2_3") > 1>>
    Hi again, {$name}.
<<else>>
    You: "Hi! I'm {$name}."
    Nadia: "Hi, {$name}! I'm Nadia."
    Nadia: "By the way, nice outfit!" <<if $name == "Charlotte">>
    <<changeConfidence(5)>>
    You: "Thanks!" (+5% confidence)
<<endif>>
<<jump 2_3_options>>
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 520,280
__PrivCrochet_tags: 
title: 2_3_options
---
What do you say?

-> "How are things here?" <<if !optionChosen()>>
    Nadia: "You're new, right?"
    Nadia: "Don't worry, it's not too hard. We're all about exploration here."
    Nadia: "There's a prompt on the board, but even that's optional if you've got a better idea!"
    <<jump 2_3_options>>

-> "What are you drawing?"

    Nadia: "Oh, I'm just drawing myself."
    <<jump 2_3_options2>>

-> "Alright, I'll leave you be then."
    Nadia: "Okay!"
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 240,520
__PrivCrochet_tags: 
title: 2_2_options2
---
Anthony shows you a picture of an anthill, with ants all around it.

-> "Are you sure you want that as your self-portrait...?"
    Anthony: "It works. I'm insignificant and should be squished."
    You: "Isn't that quite pessimistic..."
    Anthony: "It's just the truth."
    <<jump 2_2_options>>

-> "Ooh, that's a cool way to depict yourself." <<if !optionChosen()>> #skillcheck #int # 6
    <<set $diceroll = diceRoll($int)>>
    [You rolled a {$diceroll}.]
    Anthony: "What?"
    <<if $diceroll >= 6>>
        You: "Yeah. Hardworking, can lift 100 times their body weight, all that stuff." 
        Anthony: "Okay, and?"
        You: "That's just another way to interpret your art."
        You: "Why see only the bad side when there's a good side to it too?" (+10 XP)
        Anthony: "I guess so..."
    <<else>>
        You: "Uh... ants are..."
        Anthony: "Trash. Pests. Like me."
    <<endif>>
    <<jump 2_2_options2>>

-> "Is everything okay?" <<if !optionChosen()>> #skillcheck #emp # 6
    <<set $diceroll = diceRoll($emp)>>
    [You rolled a {$diceroll}.]
    Anthony: "What would you know?"
    <<if $diceroll >= 6>>
        You: "Well, I know enough to ask about people who aren't feeling too good."
        You: "But it's okay if you don't want to share."
        Anthony sighs.
        Anthony: "I'm the ant, and the world is the one squishing me."
        Anthony: "School, friends, relationship, future... all of that is super stressful to think about."
        You: "I guess you can put your feelings to paper here."
        <<changeRelationship("A", 5)>>
        Anthony: "I guess..." (+5% Anthony relationship)
    <<else>>
        Anthony: "I don't wanna talk about it."
        You: "Well, that's kinda pathetic."
        Anthony: "What?"
        You: "So emo for what?"
        <<changeRelationship("A", -5)>>
        Anthony: "Hmph. As if you care." (-5% Anthony relationship)
    <<endif>>

-> "I guess it works."
    You: "And it's your art, so shouldn't you be proud of it?"
    Anthony: "I guess so."
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 520,520
__PrivCrochet_tags: 
title: 2_3_options2
---
Nadia: "I'm still in the work-in-progress stage, though, so bear with me!"

-> "Hmm. Why did you draw in anime style?"
    Nadia: "Well... I guess it's easier, right?"
    Nadia: "After all, it was designed for 2D artists."
    You: "That's fair."
    <<jump 2_3_options2>>

-> "It's okay, I guess. Not much else to say."
    Nadia: "Alright then."
    Nadia: "Have fun with your own artwork too."

-> "You've got some hot guys on that sheet of paper too." <<if !optionChosen()>> #skillcheck #cha # 6
    <<set $diceroll = diceRoll($cha)>>
    [You rolled a {$diceroll}.]
    Nadia: "Huh? What? It's nothing! I'm just experimenting!"
    You: "Come on, I won't judge."
    <<if $diceroll >= 6>>
        Nadia: "Okay, fine... I like anime. Especially those with hot guys..."
        You: "You should take pride in that! It's your own style."
        You: "I think the drawings look good! Both your husbandos and yourself."
        <<changeRelationship("N", 5)>>
        Nadia: "Thank you... it's rare to hear that. Usually people just judge me." (+5% Nadia relationship)
    <<else>>
        Nadia: "Ugh... I'm not comfortable sharing that."
        Nadia: "Please... don't intrude on my work. I'll show it when I'm ready."
    <<endif>>
===
