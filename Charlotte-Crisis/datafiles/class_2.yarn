#__PrivCrochet_version:1
__PrivCrochet_colorID: 0
__PrivCrochet_position: 160,280
__PrivCrochet_tags: 
title: 1
---
// Enter Classroom

You arrived at the classroom with time to spare. 
It seems all your group mates are already settled down.
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 400,40
__PrivCrochet_tags: 
title: 2
---
You: "Morning!"
Sage: "Good morning."
<<if $name == "Charlotte">>
    Sage: "That's a cute blouse."

    <<changeConfidence(5)>>
    You: "Thanks!" (+5% confidence)
<<endif>>
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 400,280
__PrivCrochet_tags: 
title: 3
---
You: "Morning!"
Chenjie: "Morning! God, isn't it a pain waking up this early just for this class?"
You: "Definitely. Monday morning classes are the worst."
Chenjie: "Luckily, this class is interesting enough to be worth the effort."
You: "True!"
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 400,520
__PrivCrochet_tags: 
title: 4
---
You: "Morning!"
<<if ($name == "Charlotte") or (getRelationship("J") < 10)>>
    Joanne: "Morning..."
    You: "How was your weekend?"
    Joanne: "Nothing special." 
    She seems reluctant to talk to you.
<<else>>
    <<if visited("4") <= 1>> // only do it once
        <<changeRelationship("J", 5)>>
    <<endif>>
    Joanne: "Morning!" // (+5% Joanne relationship)
    You: "How was your weekend?"
    Joanne: "Good! Just watched a movie with my boyfriend."

    -> "Ooh! What movie?"
        Joanne: "Parasite. It's pretty good."
    
    -> "At the cinema?"
        Joanne: "No, at home. We don't really like going to the cinema."
        Joanne: "It's a bit expensive."
<<endif>>
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 640,520
__PrivCrochet_tags: 
title: chair
---
Do you want to sit down?

-> No (go back)

-> Yes (continue scene)
    <<jump 5>>
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 640,280
__PrivCrochet_tags: 
title: 5
---
Prof Lim: "Settle down, everyone!"
Prof Lim: "Okay, today, the topic will be on Gender in Beauty."
Prof Lim: "First, we shall start with a short video."

Prof starts a clip of a South Korean game show, and it quickly becomes clear what the game show is about.
Women getting plastic surgery makeovers, showcased in dramatic before-and-after photos and an energetic upbeat catwalk.
All the women are confident and happy on the runway while the crowds and judges are wow-ed by the transformation.
The video ends.

Prof Lim: "Right, we shall start with discussion questions."
Prof Lim: "First, do you think plastic surgery made the women in this show more beautiful?"
Prof Lim: "Next, who determines what beauty is?"
Prof Lim: "How does the concept of beauty affect people's lives?"
Prof Lim: "Gather in your groups and discuss this."

// Discussion arrangement
Chenjie: "Okay, so... what do you think about the women after plastic surgeries, right?"
Chenjie: "If they became prettier."
There is a slightly uncomfortable silence, like no one wants to be the first to answer.

-> "Um, they indeed became prettier."
    You: "We don't like hearing it but... plastic surgeries do in fact make people more beautiful."
    <<changeRelationship("J", 5)>>
    Joanne: "Yeah... I mean, they did the surgery in the first place, right?" // (+5% Joanne relationship)
    Joanne: "The transformations were all quite drastic. They didn't even look like the same person anymore."
    Chenjie: "I think they were also pretty in their own way before and after."
    Sage: "Yeah, but you usually only say 'in their own way' when one of them is a clear winner."
    Chenjie winces.
    Chenjie: "I guess so... It's really not nice to say though."
    Joanne: "It's reality though. Women are judged against a certain beauty standard."
    <<addExperience(5)>>
    Joanne: "We just have to live with it." (+5 XP)
    <<jump 7>>

-> "They were beautiful both before and after."
    <<changeRelationship("CJ", 5)>>
    Chenjie: "Yeah, I agree." // (+5% Chenjie relationship)
    Chenjie: "I think they are both beautiful in their own ways."
    <<jump 6>>

-> "I think natural beauty is the best, no plastic surgeries."
    You: "The world would be a lot better if there were no plastic surgeries."
    <<changeRelationship("CJ", -5)>>
    Chenjie: "Maybe not though... What about people who need the surgery? Medically speaking." // (-5% Chenjie relationship)
    Chenjie: "Personally, the women before and after are both beautiful in their own ways."
    Joanne: "Do you really think so?"
    Joanne: "No need to give the politically correct answer, you know?"
    <<jump 6>>

-> Keep silent
    <<jump 6>>
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 880,280
__PrivCrochet_tags: 
title: 7
---
Chenjie: "But what does beauty even mean?"
Joanne: "It's like ticking boxes on a checklist to say whether people are attractive or not."
Chenjie: "Yeah... And it sounds so arbitrary."
Chenjie: "They say that people with features that follow the golden ratio are beautiful."
Sage: "And facial symmetry."
Joanne: "Yeah. Another one is having healthy-looking hair."

-> "Having fair and smooth skin."
    <<changeRelationship("CJ", 5)>>
    Chenjie: "Ugh... Yeah, I looked quite ugly in secondary school because of all my acne." // (+5% Chenjie relationship)
    Joanne: "Skincare is a huge market for a reason."

-> "Being thin."
    <<changeRelationship("S", 5)>>
    Sage: "Yeah. The increase in eating disorders and slimming ads recently really hammers in those societal expectations." // (+5% Sage relationship)
    Chenjie: "It's honestly sad that it affects even kids and their self-perception... It's evil."

-> "Good fashion sense."
    Sage: "To an extent, I guess? But it's not that important, is it?"
    Joanne: "It is important actually. You can completely change your image just by dressing differently." // (+5% Joanne relationship)
    <<changeRelationship("J", 5)>>
    <<addExperience(5)>>
    Just like you, huh? (+5 XP)

<<jump 8>>
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 640,40
__PrivCrochet_tags: 
title: 6
---
Sage: "You have to admit that after the surgery, they became more conventionally beautiful."
Sage: "As much as we try to sugarcoat things."
Joanne: "Yeah, this show is sort of building on pre-existing societal expectations."
Joanne: "It's not like it invented the beauty standards by itself. There's a reason why they did plastic surgery."

-> "Yeah, it's just not really nice to say they're not beautiful..."
    Sage: "I get it, it's quite mean to say out loud..."
    Sage: "I agree, it makes you feel quite awful making these judgements about others' appearances." 
    Chenjie: "Yeah... Like, who are we to say if someone is beautiful or not?"
    <<changeRelationship("J", 5)>>
    Joanne: "It's just the harsh truth. We just have to live with the body we have." // (+5% Joanne relationship)
    <<addExperience(5)>>
    Joanne: "It's all just the luck of the draw." (+5 XP)
    <<changeConfidence(-5)>>
    Hearing this makes you a bit uncomfortable. (-5% confidence)

-> "No, they are definitely beautiful both before and after."
    <<changeRelationship("J", -5)>>
    Joanne: "There's really no need to pretend here. It's just reality." // (-5% Joanne relationship)
    Joanne: "We are here to discuss the issue, right?"
    You: "Nope, not pretending."
    <<changeRelationship("S", -5)>>
    Sage: "Okay then..." // (-5% Sage relationship)

-> Keep silent
    Chenjie: "Yeah... It's just such an awful thing to say though."
    Sage: "Yeah, no, we get it."

<<jump 7>>
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 880,520
__PrivCrochet_tags: 
title: 8
---
Joanne: "They do say that there are biological and survival reasons for some of them. But they're mostly just theories, right?"
You: "I guess so..."
Chenjie: "Moving to the next question then... Who determines what beauty is?"

-> "Society."
    Sage nods sagely. 
    Sage: "We live in a society."
    Joanne: "That's basically a non-answer. Obviously it's society, but what part of it?"

-> [INT 7] "Misogyny. Specifically..." ({calculateOdds($int, 7)}% success) #skillcheck #int
    <<set $diceroll = diceRoll($int)>>
    [You rolled a {$diceroll}.]
    <<if $diceroll >= 7>>
        You: "The patriarchy."
        You: "Because patriarchy is the social system where men and masculinity have more power, right?"
        <<changeRelationship("S", 5)>>
        Sage: "Hm... Yeah..." // (+5% Sage relationship)
        <<addExperience(5)>>
        Sage: "Our readings do point out the concept of power obtained and maintained by policing the behaviours and bodies of the people." (+5 XP)
        Chenjie: "Wait, could you explain?"
        Sage: "Basically, you maintain a male-dominated society by making sure people don't break the rules of that society."
        You: "Yeah, so women have to look pretty for the male gaze in order to stay in line."
        You: "And men have to look strong and manly to stay worthy of being a man."
        <<changeConfidnce(5)>>
        Chenjie: "Yeah, I guess that makes sense." (+5% confidence)
    <<else>>
        You: "Men."
        Joanne: "I guess so?"
        Joanne: "But I don't think it's just men."
        Joanne: "I mean, don't women have a huge role to play in the beauty standards of men?"
        Chenjie: "True..."
        Chenjie: "I think you were right about it being misogyny though, {$name}."
        Chenjie: "I think it's the people who want to make sure men and women have their respective roles to play in society."
        Chenjie: "So that includes how men and women should look as well."
    <<endif>>
    <<if $name == "Charlotte">>
        <<jump 9>>
    <<else>>
        <<jump 17>>
    <<endif>>

-> "History and culture"
    Sage: "Yup! History and culture for sure."
    Sage: "Like the way fair skin historically is associated with wealth because you're not tanned from working in the sun."
    Chenjie: "It's interesting how the standards of beauty change too."
    Chenjie: "Being fat used to be a sign of health and beauty."
    You: "And pink used to be the colour for boys. But now, pink is a colour for girls."
    <<changeRelationship("CJ", 5)>>
    Chenjie: "Wait, for real?" // (+5% Chenjie relationship)
    You: "Yeah! You can look it up!"
    You: "It's because they thought red is a man's colour, so the lighter version is for the boys."
    Chenjie: "But now if a guy wears pink, people start questioning his sexuality."
    Chenjie: "There aren't a lot of things that are acceptable for men to wear."
    <<if $name == "Charlotte">>
        <<jump 9>>
    <<else>>
        <<jump 17>>
    <<endif>>
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 1120,520
__PrivCrochet_tags: 
title: 9
---
Sage: "The beauty standard for women is definitely much narrower than for men."
Sage: "Women are more likely to be criticised for their appearances."
You: "Yeah, there are tons of criteria women usually have to meet before—"
Joanne: "God, I've had enough!"
Joanne: "Look, are we just going to straight up ignore the fact that you are a complete affront to womanhood?"
You: "What...?"
Joanne: "Us girls get made fun of for not looking girly enough, or looking too girly, or being too fat or not curvy enough."
Joanne: "And you get to just dress up and pretend to be a girl for fun?"

-> "I know women face a lot of unfair criticism and scrutiny for their appearance."
    You: "We need to stop forcing unrealistic beauty standards on women, and learn to accept the diversity in women."
    You: "And I don't think my crossdressing works against this."
    <<if getRelationship("CJ") >= 30>>
        Chenjie: "Yeah, I don't think it's been easy for Charlotte either..."
        Chenjie: "In a way, maybe she experienced some of the stuff you did too. Especially as a more masculine-looking person."
        Chenjie: "No offence."
        You know he means well. (+5% Chenjie relationship)
        Joanne: "It's not the same. He can just stop being female whenever it's convenient?"
        Joanne: "Why does he get to just take off all his makeup and go back to being a dude whenever he wants?"
        Chenjie: "Well... uh..."
        Chenjie tries to think of a refutation, but in the end, all he could come up with is...
        Chenjie: "I don't think it's that simple though...")
    <<else>>
        Sage: "Yeah, and I'm sure she has also faced a similar kind of scrutiny while she was presenting as Charlotte."
        Sage: "In a sense, she's the same as us."
        Joanne: "It's not the same! He can just stop being female whenever it's convenient."
        Joanne: "Why does he get to just take off all his makeup and go back to being a dude whenever he wants?"
        Sage: "It doesn't work that way!"
        Joanne: "How so?"
        Sage frowns, eyes twitching with anger, but she does not utter a word.
    <<endif>>

-> "I'm not pretending to be a woman."
    You: "I'm not trying to trick or mock anyone. That's not my intention."
    You: "It's just that dressing like this feels natural and comfortable for me."
    Joanne: "And what exactly is so natural and comfortable about it?"
    Joanne: "You literally have no idea how much women like Sage and I had to suffer just by being women."
    <<if ($emp >= 4) or (getRelationship("S" >= 30)>>
        Sage squirms in uneasiness.
    <<endif>>
    Joanne: "I was always made fun of in school because I was boyish. And I had to deal with periods, and makeup, and clothes."
    <<addExperience(5)>>
    Joanne: "After women have suffered so much for the way they look, why do you get to feel comfortable dressing up like one?" (+5 XP)
    Chenjie: "But don't we want everyone to feel comfortable however they choose to dress?" 
    Joanne: "Okay, so a man's comfort should be prioritised over the very real problems women face." 
    <<changeConfidence(-5)>>
    <<changeRelationship("J", -5)>>
    Joanne: "Amazing." (-5% confidence) // (-5% Joanne relationship)

-> [EMP 8] "We are fighting the same fight." ({calculateOdds($emp, 8)}% success) #skillcheck #emp
    <<set $diceroll = diceRoll($emp)>>
    [You rolled a {$diceroll}.]
    <<if $diceroll >= 7>>
        You can almost see it.
        The image of a younger Joanne — struggling in her femininity, criticised for the way she looked and dressed, trying to find her place.
        She is a lot like you in some ways. 
        But she struggles to see that because most of her life, she understood the world as men vs women. 
        <<addExperience(10)>>
        Arguing with her any further would only make her more defensive. You see that now. (+10 XP)
        You take a deep breath.
        You: "I'm sorry you had to deal with so much criticism and hardships growing up as a girl." 
        You: "It's awful that girls and women are constantly pressured to reach an impossible standard."
        <<changeRelationship("J", 5)>>
        Joanne: "Yeah, we shouldn't be expected to all look like supermodels or like we've done plastic surgery." // (+5% Joanne relationship)
        Joanne: "My family also frequently criticises me for not wearing any makeup, and say that I'm not putting any effort into myself."
        You: "There are endless ways to be feminine, and all of them are valid." 
        You: "Why can't we be accepted for the way we look, whether it's by our own choice or because we were born that way?"
        You: "Aren't we essentially all fighting for acceptance for the way we express our femininity?"
        Joanne hesitates.
        <<changeConfidence(5)>>
        You: "Maybe we won't see eye to eye on this. But this can be just food for thought." (+5% confidence)
        Joanne: "Fine."
        Chenjie: "Maybe we should get back to the discussion questions?"
        <<jump 10>>
    <<else>>
        Joanne: "How so?"
        You: "You want women to be less criticised for the way they look, right?" 
        You: "Why not a woman who looks like a man or isn't conventionally feminine?"
        Sage: "Yeah... I mean, isn't the whole point to be more accepting of all kinds of women?"
        Joanne: "There has to be a limit." 
        Chenjie: "Why?"
        <<changeRelationship("J", -10)>>
        Joanne: "Because men like him are a threat to what it means to be a woman. They dismiss all our sufferings." (-10% Joanne relationship)
        Joanne: "He's encroaching the safe spaces built for women to avoid having to deal with sexist men in the first place."
        
        -> "I'm not trying to hurt anyone."
            You: "I'm also trying to live my life. I'm not hurting anyone by simply existing, am I?"
            Joanne: "You are disrespecting women."
            Sage: "I don't think dressing femininely is a disrespect."
            Sage: "Aren't we all just human beings trying to survive and find a way to live in this world? Why can't we treat others with more kindness and understanding?"
            Joanne: "I don't think I can understand people like him."
            Chenjie: "You can still treat someone with kindness even if you don't understand them, right?"
            Chenjie: "You don't have to understand someone's religion in order to respect them, right?"
            Joanne: "Ugh! I give up! This is a three-against-one anyway." 
            Joanne huffs and crosses her arms.
            It looks like that for the rest of the discussion, she won't be speaking.
            Chenjie looks a bit at a loss. 
            Chenjie: "Let's just... uh... continue with the class discussion."
            <<jump 10>>
            
        -> "How does crossdressing dismiss your sufferings?"
            Joanne: "It's not just crossdressing though, is it? It's that you are saying you're a woman."
            Joanne: "You're speaking over the voices of real women."
            You never once said to her that you were a woman. 
            
            -> "Don't put words in my mouth."
                You: "I only said that I'm a crossdresser."
                You: "Yes, I'm questioning my gender, but I never claimed once to be a woman."
                Joanne: "So what?  You're still speaking over real women."
                <<changeConfidence(-5)>>
                Joanne: "I don't care if your gender is attack helicopter or whatever. People like you should be locked up in IMH." (-5% confidence)
                <<changeRelationship("S", -5)>>
                Sage stands up abruptly. // (-5% Sage relationship)
                Sage: "I need to use the toilet."
                // ((Maybe have an animation of Sage leaving the room with no dialogue.))
                Joanne: "Was what I was saying so wrong?"
                Chenjie: "Let's... uh... just continue without her first...?"
                <<jump 11>>
        
            -> "How so?"
                You: "I'm not dismissing your experiences with misogyny or sexism."
                You: "I just want to add my own experiences as someone whose experience is different as a crossdresser."
                Chenjie: "Yeah, let's not dismiss any of the experiences and opinions brought up here."
                <<addExperience(5)>>
                Sage: "In some ways, Charlotte may have some insight into gender and sexism that we haven't heard of." (+5 XP)
                <<changeConfidence(5)>>
                Sage: "And she probably has faced a lot of scrutiny while crossdressing too so if anything, I think she also understands how unfair beauty standards are." (+5% confidence)
                Joanne: "Ugh! I give up! This is a three-against-one anyway."
                Joanne huffs and crosses her arms.
                It looks like that for the rest of the discussion, she won't be speaking.
                Chenjie looks a bit at a loss.
                Chenjie: "Let's just... uh... continue with the class discussion."
                <<jump 10>>
        
        -> Stay silent
            <<changeRelationship("S", 5)>>
            Sage: "I don't think she is." // (+5% Sage relationship)
            Sage: "I think people like Charlotte deserve a safe space as much as cis women do."
            Sage: "If anything, they face a lot of discrimination and they have to work hard to pass as women so they don't get targeted by bigots."
            Chenjie: "Yeah, let's treat each other with kindness and respect."
            Chenjie: "I think you've really hurt Charlotte with your remarks."
            Joanne: "Ugh! I give up! This is a three-against-one anyway."
            Joanne huffs and crosses her arms.
            It looks like that for the rest of the discussion, she won't be speaking.
            Chenjie looks a bit at a loss.
            Chenjie: "Let's just... uh... continue with the class discussion."
            <<jump 10>>

-> "I'm not going to argue with you about this."
    Joanne: "Yeah, typical."
    Joanne: "Running away from an argument you know you can't win." 
    You try to tell yourself that there is no shame in refusing to argue with someone who refuses to change their mind.
    Why exhaust yourself with a fruitless argument?
    <<changeConfidence(-5)>>
    But it doesn't work that well. (-5% confidence)
    <<changeRelationship("CJ", 5)>>
    Chenjie: "Hey, I don't think it's been easy for Charlotte either..." //(+5% Chenjie relationship)
    Joanne: "He can just stop being a 'girl' whenever he wants. How is it not easy for him?"
    Sage: "Stopping is not as straightforward as you think."
    <<addExperience(5)>>
    Sage: "Gender dysphoria is something that is deeply uncomfortable, and questioning your identity takes a lot of work and courage." (+5 XP)
    Sage: "There's really no harm in being compassionate to people, even if you don't 100% understand their troubles."
    Joanne: "Ugh! I give up! This is a three-against-one anyway."
    Joanne huffs and crosses her arms.
    It looks like that for the rest of the discussion, she won't be speaking.
    Chenjie looks a bit at a loss.
    Chenjie: "Let's just... uh... continue with the class discussion."
    <<jump 10>>
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 1120,280
__PrivCrochet_tags: 
title: 10
---
The rest of the discussion proceeds with no other major hiccups, but the atmosphere is greatly soured.
//Lecture arrangement//
Prof Lim: "Alright, class! Let's hear what everyone has to say?"
Prof Lim: "What about that group over there?"
She's talking about your group.
Your groupmates freeze up.
You: "We talked about..."

-> "How unfair the beauty standards are."
    You: "The beauty standards are also constantly changing with time."
    Chenjie: "Yeah, and the beauty standards are way narrower for women too. Like, a double standard."
    Prof Lim: "Hm... Yes. That's true." 
    Prof Lim elaborates on the double standards of beauty before moving on to the next group.

-> "How beauty standards adversely affect people's lives."
    You: "Like hiring discrimination, makeup culture, pretty privilege, and so on."
    <<changeConfidence(5)>>
    Prof Lim: "Yes, that is true. Unrealistic beauty standards do in fact have adverse effects on people's daily lives." (+5% confidence)
    Prof Lim elaborates on the idea of hiring discrimination based on physical appearances before moving on to the next group.

-> "How crossdressers and trans people are an affront to women." <<if visited("9")>>
    You: "Or at least, that's what one of my groupmate feels."
    The class is stunned into silence by the venom with which you said that.
    Prof Lim: "Oh, well, it seems your group must have sidetracked a little?"
    <<changeRelationship("CJ", -5)>>
    Chenjie: "But we also talked about the beauty standard stuff!" // (-5% Chenjie relationship)
    Chenjie goes into some of the talking points brought up during the discussion.
    Prof Lim: "I see. Those are all very good points."
    Prof Lim: "Class, we should remember to respect each other's opinions and identities."
    Prof Lim: "No personal attacks during discussion."
    Your groupmates look at you in a mix of disappointment and frustration. 
    <<changeConfidence(-5)>>
    That did not go as you had hoped. (-5% confidence)

<<jump 13>>
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 1360,40
__PrivCrochet_tags: 
title: 13
---
Lecture proceeds uneventfully after that. (+ 5 XP)
Prof Lim: "And that's it for class! Thank you, everyone."
That's it for class today. 
Who will you walk with today?

-> Go back alone.
	You don't seem comfortable enough to talk more with any of your classmates.
	You decide to head back to the hostel by yourself.
	Slinking away to the door, you make a quiet exit.
    <<goto_room_bus()>>

-> Chenjie <<if getRelationship("CJ") >= 10 >>
    <<jump 14>>

-> Joanne <<if getRelationship("J") >= 10 >>
    <<jump 15>>

-> Sage <<if getRelationship("S") >= 10 >>
    <<jump 16>>
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 1360,520
__PrivCrochet_tags: 
title: 11
---
//Lecture arrangement//
However, Sage does not return even after the discussion.
Prof Lim approaches your table.
Prof Lim: "Sage has been gone for quite a while..."
Chenjie: "She said she's going to the toilet..."
Prof Lim: "Perhaps, someone should check on her."

-> Volunteer to go
    You: "I'll go."
    <<changeRelationship("J", -5)>>
    Joanne: "Don't be ridiculous. I'm a girl. I'll go." // (-5% Joanne relationship)

-> Stay silent
    Joanne: "I'll go check on her."

<<jump 12>>
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 1360,280
__PrivCrochet_tags: 
title: 12
---
//black screen//
Joanne leaves to find Sage.
Sage might not want to see Joanne... It looked like she left because of what Joanne said.
Sure enough, a minute later, Joanne comes back alone.
//classroom screen//
Joanne: "She said she'll be back in a while."
Chenjie: "How is she?"
Joanne: "... She sounded quite upset... I don't know..."
When Sage returns to the class, Prof Lim is explaining lookism in South Korea.
She looks a bit exhausted.

-> "Are you okay?"
    <<if getRelationship("S") >= 40>>
        Sage: "What Joanne said reminded me of something bad."
        Sage: "I'm better though. Thanks for worrying."
        You: "Okay... If there's anything you need me to do for you, let me know?"
        <<changeRelationship("S", 5)>>
        Sage nods. // (+5% Sage relationship)
    <<else>>
        Sage: "Yeah. Just a stomachache."
        Sage: "Must have eaten something bad this morning."
        You: "Get well soon."
        <<changeRelationship("S", 5)>>
        Sage: "Thanks." // (+5% Sage relationship)
    <<endif>>

-> Leave her alone

<<jump 13>>
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 1600,40
__PrivCrochet_tags: 
title: 14
---
Chenjie: "Yeah, sure."

<<goto_room_bus_cj()>>
//Bus stop//
Chenjie: "Beauty standards, huh..."
Chenjie: "We mostly talked about beauty standards for women today, but..."
Chenjie: "I wonder though... The standard for dudes."

-> "Guys have it way easier."
    Chenjie: "I mean, it's not a competition."
    Chenjie: "I just think we have it different from girls."

-> "It's honestly not much better."
    <<changeRelationship("CJ", 5)>>
    Chenjie: "Yeah, we have different kinds of standards we are expected to meet." // (+5% Chenjie relationship)

-> "Honestly, guys have it worse."
    You: "Ugly guys don't even get the time of day sometimes."
    <<changeConfidence(-5)>>
    You: "Women don't spare a single glance if you don't look like the ideal partner." (-5% confidence)
    Chenjie: "I mean... I feel that while guys don't exactly have it worse, we have it worse in some ways."

Chenjie: "I feel like men are expected to be big, tall and buff or something..."
Chenjie: "I get criticised for that by some of my relatives sometimes."
Chenjie: "I'm too lanky, and I just feel that no girl will be interested in a beanpole."
You: "What brought this up?"
Chenjie: "I don't know... It's just that I've never actually been in a serious relationship before even though it's literally my last schooling year."
Chenjie: "And so today's lecture got me thinking about, well, if my physical appearance has something to do with it."

-> "What constitutes a serious relationship?"
    Chenjie: "I guess saying officially that you are going out and dating?"
    Chenjie: "So far, I've only ever gone out on maybe one or two dates with some girls."
    Chenjie: "But that's about it..."
    <<if $emp >= 5>>
        He's keeping his descriptions pretty vague...
        <<addExperience(5)>>
        He mustn't like talking about his love life. (+5 XP)
    <<endif>>

-> [CHA 6] Try to offer advice ({calculateOdds($cha, 6)}% success) #skillcheck #cha
    <<set $diceroll = diceRoll($cha)>>
    <<if $diceroll >= 6>>
        You: "I think you should build up your self-confidence. Don't beat yourself up for something like this."
        You: "You are a cool and kind guy so it's a shame that you don't see how amazing you are."
        You: "Besides, people are naturally drawn to those who carry themselves with confidence."
        You: "So you just have to have faith in yourself, and good things will follow."
        Chenjie smiles.
        <<changeConfidence(5)>>
        Chenjie: "That's nice to hear." (+5% confidence)
        The bus arrives.
        Chenjie: "Bye! See you!"
        You: "See you!"
    <<else>>
        You: "Maybe you could get buff. Go to the gym."
        <<changeRelationship("CJ", -5)>>
        Chenjie: "You sound like one of my aunties or something. I literally go to the gym every other day already." // (-5% Chenjie relationship)
        You: "Oh... Maybe train different parts?"
        Chenjie: "I've tried, okay?"
        Chenjie: "It's not as easy as you think to build muscles in the right places."  
        You: "I see..."
        Chenjie: "Look. My bus is here. See you next week."
        You: "Okay, bye."
    <<endif>>

-> "You're a good guy. I'm sure you'll get a girl soon."
    You: "You gotta trust."
    Chenjie: "I guess."
    The bus arrives.
    Chenjie: "Bye! See you!"
    You: "See you!"

<<goto_room_bedroom()>>
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 1600,280
__PrivCrochet_tags: 
title: 15
---
Joanne: "Yup, okay. Wait while I pack up..."

<<goto_room_bus_j()>>

Joanne: "My boyfriend's birthday is coming up soon."
Joanne: "We've been together since secondary school but I still always struggle with buying him gifts..."
You: "Coming up with a birthday gift is always tricky, isn't it?"
You: "It's hard to predict what someone most wants to receive at the moment."
Joanne: "Right now, I'm deciding between headphones, a limited edition song album, and a pair of sneakers."
Joanne: "As a guy, what kind of gift would you appreciate most from your girlfriend?"

-> Headphones
-> Limited edition album
-> A pair of sneakers

Joanne: "Hm..."
Joanne: "Several guys have told me that too..."
Joanne: "I mean, I didn't think my boyfriend would appreciate that."

-> "You know your boyfriend best."
    You: "You've been dating him for years, right?"
    You: "Each person is different so use your judgement of his likes and dislikes."
    Joanne: "True..."
    You: "Have faith in yourself and your judgement." 
    <<changeRelationship("J", 5)>>
    Joanne: "Hm, you're right. I do know him best. He liked all my gifts in the past too." // (+5% Joanne relationship)
    Joanne: "Oh, the bus is here."
    <<changeConfidence(5)>>
    Joanne: "Thanks for the advice. Bye!" (+5% confidence)
    Joanne: "Bye!"

-> "In the end, it's the thought that matters."
    You: "Whatever you get for him, I'm sure you put a lot of thought into it."
    You: "The act of giving is the act of love in itself."
    Joanne: "True. Usually, it matters more to me that I'm getting a present than the actual present itself."
    Joanne: "Some years ago, I received a Cinnamoroll keychain, and now it's my favourite Sanrio character." 
    <<addExperience(5)>>
    You: "It's nice just to know that someone else thought of you, and what you would like." (+5 XP)
    You: "Oh, is that your bus?"
    Joanne: "Oh, yes!"
    Joanne: "But thanks for your help. I know what to get for my boyfriend now."
    You: "No problem. Bye!"

-> "Majority vote, am I right?"
    Joanne: "Yeah... I'm not a great gift giver..."
    You: "I always struggle to figure out what others want..."
    You: "I know people who keep a list of things their friends have mentioned needing or wanting over the year."
    Joanne: "That's also a good idea! But... well..."
    Joanne: "It's especially hard with guys because with girls I can just give a cute keychain or something."
    Joanne: "But with guys, there just aren't that many things I feel they would like."
    You: "Oh, is that your bus?"
    Joanne: "Oh, yes!"
    Joanne: "But thanks for your help. I know what to get for my boyfriend now."
    You: "No problem. Bye!"

<<goto_room_bedroom()>>
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 1600,520
__PrivCrochet_tags: 
title: 16
---
Sage: "Okay, let's go then."

<<goto_room_bus_s()>>

//Bus stop//
You: "How has your weekend been?"
Sage: "Fine. I was a little busy helping a friend move house."
Sage: "Who knew moving all that stuff would be so tiring? Even with a car."
You: "Move house?"
Sage: "Yeah, or more like moving to a rented room."

-> "How old are they?"
    Sage: "He's 22 this year."
    You: "That is quite young."
    Sage: "He's quite independent. He's always wanted to move out as soon as he was financially able."
    You: "So he's working full-time already?"
    Sage: "Sort of..."

-> "Why did they ask you and not a mover?"
    Sage: "He's my friend from primary school and we've stuck together ever since."
    Sage: "You know, trying to save some money. And it's not that much stuff anyway."
    You: "What about his family though?"
    Sage: "They were... busy."

-> "They were moving out of their parents' house?"
    Sage: "I guess you could say so."
    Sage: "It's complicated... He had a bit of a fight with his parents, and that's why he had to move out."

Sage seemingly regrets speaking of this at all.
<<if ($emp >= 6) or (getRelationship("S") >= 40)>>
    <<addExperience(5)>>
    Like she had accidentally spilled someone else's secret. (+5 XP)
<<endif>>

Sage: "What about you though? How's your weekend been?"

-> "I had a talk with my sister about my crossdressing."
    //if boss1 ending 2
    You: "It went well. We had a good talk about my hobby and what it means to me."
    Sage: "So she was accepting?"
    You: "Yeah!"
    Sage: "That's a relief. I'm glad you have such a supportive sister." (+5% Sage relationship)
    You: "I still have my mum to convince..."
    Sage: "Yeah, it can be harder with parents..."
    Sage: "And that's my bus! But all the best, okay?"
    You: "Yeah, bye!"
    // end

    //else 
    You: "It... went poorly. She doesn't really like that I crossdress."
    Sage: "Oh dear... I'm so sorry for that."
    Sage: "Take care, okay? I know it's tough to face rejection like that..."
    You: "I hope things will get better with time... I wish there were more people who understood."
    Sage: "I don't know if things will get better necessarily but... I think you'll definitely find people who understand." (+5% Sage relationship)
    Sage: "You just have to know where to look." (+5% confidence)
    You: "Thanks..."
    Sage: "That's my bus. All the best, okay? Take care." 
    You: "Bye."
    // end


-> "It was okay. I didn't do much."
    You: "I did some work, I guess."
    Sage: "I see... Good for you."
    Sage: "Oh, that's my bus."
    Sage: "Good talk, okay? Uhm, see you next week!"
    You: "Bye."
    // ((END))

-> "I started reading this non-fiction book."
    You: "It's called 'The Body Keeps the Score.'"
    Sage: "I think I've heard of it... Is it good?"
    <<addExperience(5)>>
    You: "Yeah, it's been interesting so far. It really explains in detail the effects of past experiences on a person's mind and body." (+5 XP)
    Sage: "Huh. That is fascinating... I originally wanted to take psychology."
    Sage: "But after taking the intro course, it felt like it was more fun as something to look into by yourself, and not as an examinable subject." 
    <<changeRelationship("S", 5)>>
    You: "Yeah, I get that sentiment." (+5% Sage relationship)
    Sage: "Oh, that's my bus. See you next week!"
    You: "Yeah, see you!"
    // end

<<goto_room_bedroom()>>
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 880,760
__PrivCrochet_tags: 
title: 17
---
Chenjie: "Then, next... How does the concept of beauty affect our daily lives..."
Joanne: "Finding jobs... Studies show that more conventionally attractive people tend to have better hiring experiences, right?"
Chenjie: "Right... Like pretty privilege is a real thing."
Chenjie: "I know someone who had a glow-up and apparently the way people treat her is very different from before."
Chenjie: "And since we talked about plastic surgery, there's also makeup culture..."
Sage: "There's also this obsession with the no makeup look."
Sage: "Like, it's weird that the guys who are the most adamant that no makeup is the best are also the ones who are only nice to pretty looking girls."
Sage: "And somehow, us girls have to achieve that no makeup look while putting on makeup."
Joanne: "God, I get it completely. I hate it when my mother keeps nagging at me to put on makeup."
Joanne: "And makeup is actually so bad for your skin. Just caking your face with who-knows-what chemicals every day."

-> "Makeup helps to increase self-confidence though."
    <<addExperience(5)>>
    You: "Maybe making such generalisations isn't helpful. What about the people who wear it as an art form or to boost their self-esteem?" (+5 XP)
    <<changeRelationship("J", 5)>>
    Joanne: "Yeah, I don't think it's all bad either." (+5% Joanne relationship)
    Sage: "It's up to you whether you want to wear makeup. But when it becomes a social expectation, it really becomes quite toxic." 
    Chenjie: "It is unfortunately ingrained in our culture... People are generally superficial."
    <<jump 10>>

-> Talk about your experiences with makeup
    You: "Makeup helped me find myself... And I find it quite fun to apply makeup. Especially the colourful ones."
    Joanne: "You're still wearing makeup these days...?"

    -> "No. Back when I still dressed as Charlotte, but not anymore."
        Sage: "You're not dressing as Charlotte anymore?"
        Chenjie: "Why?"
        You: "Yeah... I decided to stop. Enough is enough."
        Sage: "I see... I mean, I guess that's... an understandable choice."
        Chenjie: "Aren't you happier as Charlotte though?"
        Who would ever feel happy holding a part of themself back out of fear and shame? 

        -> Lie
            You: "No, I feel better as Charles I think."
            You: "Charlotte is the thing of the past now. More like a phase."
            Sage: "Really?"
            You: "Yeah, I mean, I honestly have no idea why I did all that. That was a weird time."
            Joanne: "Oh, that's good. I'm glad you've gotten better and figured things out."
            <<changeConfidence(-10)>>
            It doesn't feel good to tell such a blatant lie. (-10% confidence)
        
        -> Tell a half truth
            You: "I mean, I like being Charlotte but in the end, I'm choosing to be Charles."
            You: "Being Charlotte taught me a lot about myself, but right now, I just want to be Charles."
            You: "And I'm perfectly happy with that conclusion."
            Chenjie: "Okay, good that you're happy!"
            <<changeConfidence(-5)>>
            Saying all that didn't feel good. It was like lying to yourself. (-5% confidence)
            Sage looks at you in concern.

        -> Tell the truth
            You: "I decided to stop after my sister found out..."
            You: "I just don't think Charlotte is worth tearing apart my personal relationships and my own future for."
            You: "I think it's best moving forward to just leave Charlotte in the past."
            Sage: "Oh... I... That sounds really tough." 
            Chenjie: "It's your own choice though. It's your own body and life."
            Chenjie: "You deserve to be living in a body and life that makes you happy." 
            <<changeConfidence(5)>>
            You: "I know, but I weighed the pros and cons and living as Charles 24/7 is the best for me." (+5% confidence)
            Chenjie: "If you're sure then."

        <<jump 10>> // go to 10 after the above 3 options

    -> "Yes, when I dress up as Charlotte."
        You: "It's difficult if I don't wear makeup because I pass even less."
        You: "Wearing makeup helps send a signal of femininity. And contouring my face helps."
        Chenjie: "I mean, it makes sense that wearing makeup is a confidence booster."
        <<changeRelationship("S", 5)>>
        Sage: "It kind of sucks that you have to put on makeup to even have your femininity acknowledged." // (+5% Sage relationship)
        You: "I feel a huge pressure to apply makeup perfectly too."
        You: "Because if I don't I'm not doing femininity well enough to qualify."
        <<addExperience(5)>>
        Sage: "I think that feeling is common for cis women too." (+5 XP)
        <<jump 9>>

-> Stay silent
    Chenjie: "Makeup culture is both a product and also further feeding into the unfair beauty standards."
    Joanne: "It is a cycle of sorts..."
    Joanne: "And sometimes, when you bring it up, people will say you're complaining because you're just jealous."
    Chenjie: "Jealous?"
    Joanne: "Like, jealous of pretty people, or people who know how to apply makeup well."
    Sage: "That happened to you?"
    You: "God, that sucks..."
    <<jump 10>>
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 1600,760
__PrivCrochet_tags: 
title: 12_alone
---
You slink away, alone again.
<<goto_room_bedroom()>>
===
