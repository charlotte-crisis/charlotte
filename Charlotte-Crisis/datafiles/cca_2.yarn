#__PrivCrochet_version:1
__PrivCrochet_colorID: 0
__PrivCrochet_position: 0,-40
__PrivCrochet_tags: 
title: 1
---
You're a little early to the session today, and so are Anthony and Vera.
They are sitting on opposite sides of the room. Seems like last session wasn't enough to bring them together.
Behind his laptop, Anthony buries his head in whatever he's drawing, occasionally looking up at his screen.
Vera is scrolling through her phone casually.
Go talk to them before the session starts!

<<declare $satWithAnthony = false>>
//this scene should only have MC, Vera and Anthony
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 480,240
__PrivCrochet_tags: 
title: 2
---
[i]Nadia and Eleanor enter the room together.[/i]
Eleanor: "Oh, you guys are here already!"
Nadia: "Guess we didn't need to rush."
Eleanor: "Alright! But... this is all we have?"
Vera: "Seems like it."
Eleanor: "Ah... I was expecting a few more people. This could have been a group activity."
Anthony: "What do you want us to do?"
Eleanor: "We're designing a T-shirt for the Art Club!"
Anthony: "Really? Only the 4 of us show up regularly and you still want a T-shirt?"

-> "Yeah, I'm not sure this is a good idea." //(+5% Anthony relationship)
    <<changeRelationship("A", 5)>>
    Eleanor slumps her shoulders a bit. She struggles to keep her face unchanged.
    Then, she shrugs.
    Eleanor: "Fine, hypothetically, we could make a T-shirt."
    Eleanor: "Those who are against the idea of a shirt, maybe... treat this as just any other graphic design exercise?"
    Nadia: "Sounds good to me."

-> "This could be the start of something new."
    Anthony: "What do you mean?"
    <<addExperience(10)>>
    You: "I mean, we've got to believe in ourselves because no one else will, right?" (+10 XP)
    You: "So maybe if we've got a cool shirt, people will ask who we are, and more people will be interested to join."
    Eleanor: "That's a great way to see it! Thanks, {$name}."

<<jump 3>>
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 720,240
__PrivCrochet_tags: 
title: 3
---
Eleanor: "Alright then, let's design for about 30 minutes and we can share."
[i]You scour the Internet for ideas, eventually settling between three choices.[/i]

-> A paint blob with "Art Club" in the middle.
    <<set $logo = "blob">>
    Avant-garde, yet simple.
    You try not to think about how exactly you want to draw the blob, and put it to paper.

-> A palette and paintbrush.
    <<set $logo = "palette">>
    People will definitely associate this one with "art".
    You quickly sketch out the silhouette and touch up its various details.

-> A sunset.
    <<set $logo = "sunset">>
    You didn't need the Internet for this one.
    Sunsets have always held this allure for you. You love to appreciate their beauty at the end of a long day.
    And isn't that what art is about? Not just the final product, but the journey there.

<<jump 4>>
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 959,237
__PrivCrochet_tags: 
title: 4
---
Eleanor: "Alright, are we ready to share?"
[i]Everyone in the room looks up.[/i]
Eleanor: "Okay, let's get started! {$name}, would you like to show us what you've drawn?"
You: "Alright, here goes..."

<<if $logo == "blob">>
    Anthony: "Uh... what am I looking at?"
    Vera: "Looks like a puddle."
    Nadia: "No, I think it's a blob of paint."
    You: "That's right."
    Eleanor: "If anything, it looks like the Nickelodeon logo. I think it's workable, but let's see if there's anything else?"
<<elseif $logo == "palette">>
    Nadia: "Okay, this one really says 'Art Club'. Very direct."
    Anthony: "Reminds me of Microsoft Paint."
    Vera: "We don't just do painting, though, right?"
    Eleanor: "Nadia has a point. An easily identifiable logo would be good on a T-shirt. Moving on..."
<<else>>
    Vera: "Looks like I'm not the only one with a weather feature for a logo."
    Nadia: "What do you mean?"
    Vera: "You'll see."
    Anthony: "How is a sunset related to art club? Are you saying we're going under?"
    -> "Yeah. Bye!" #skillcheck #cha #8
        <<set $diceroll = diceRoll($cha)>>
        [You rolled a {$diceroll}.]
        <<if $diceroll >= 6>>
            [i]You expected people to take issue with that, but Eleanor laughs.[/i]
            <<changeConfidence(5)>>
            Eleanor: "As if we're going anywhere." (+5% confidence)
            Eleanor: "I like the concept! Colouring the gradient will be hard, but we can save that for another day."
        <<else>>
            Anthony: "Knew it. I guess we don't believe in this organisation anymore."
            Eleanor: "That's not what it means!"
            <<changeConfidence(-5)>>
            [i]Eleanor shoots you a quick glare.[/i] (-5% confidence)
        <<endif>>
        
    -> "Sunsets are nature's art." #skillcheck #int #8
        <<set $diceroll = diceRoll($int)>>
        [You rolled a {$diceroll}.]
        <<if $diceroll >= 8>>
            <<addExperience(10)>>
            You: "And when do we see the sunset? After a long day, we truly appreciate its natural beauty." (+10 XP)
            Nadia: "Wow, that's deep!" 
            Anthony: "Fair point."
            Vera: "Hmm, do rainbows and sunsets come together?"
            Eleanor: "I guess we can search for that later. Moving on..."
        <<else>>    
            Anthony: "Don't just say that to sound pretentious."
            You: "Uh... I'm not sure. Sunsets look pretty."
            <<changeConfidence(-5)>>
            Anthony: "Many other things look pretty. Yours isn't that special." (-5% confidence)
            Vera: "Hate to say it, but yeah, I agree."
            Eleanor: "Then let's see what other pretty things you've drawn!"
        <<endif>>

    -> "Uh... I'm not sure. Sunsets look pretty."
        <<changeConfidence(-5)>>
        Anthony: "Many other things look pretty. Yours isn't that special." (-5% confidence)
        Vera: "Hate to say it, but yeah, I agree."
        Eleanor: "Then let's see what other pretty things you've drawn!"

<<jump 5>>
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 1198,238
__PrivCrochet_tags: 
title: 5
---
Anthony: "Okay, here's mine."
Vera: "Wow, are those paintbrushes wearing ties?"
Anthony: "Yep."
Nadia: "Since when did we wear ties?"
Eleanor: "Maybe I should make that a rule."
Anthony: "Please don't."
Nadia: "I think I'd look good in a tie. But like, Avril Lavigne style, so with a tank top."
Vera: "That would be so slay!"
Eleanor: "Jokes aside, I'm a bit undecided about the ties."
Anthony: "I mean, that's what logo finder dot com gave me when I put in the prompt."
Vera: "You used AI to generate this?"
Anthony: "What? Normal what. Now everyone is using ChatGPT for their assignments, right?"
Nadia: "Touché. {$name}, what do you think?"
// Try touché without accent if cannot
-> "I think it's cheating."
    <<changeRelationship("A", -5)>>
    Anthony: "Wow. Way to go, so direct." (-5% Anthony relationship)
    Nadia: "Now that you mention it, I've seen so many anime fan artists get their work copied by AI."
    Vera: "I just gave it a shot and damn, these logos are so generic and corporate."
    Anthony: "Well, I guess that's what it was meant for, right?"
    Vera: "Then why did you use it in a club that's anything but that?"
    Eleanor: "Vera, please. I think there are some aspects we can keep. Moving on..."

-> "I think it's justifiable."
    Vera: "Seriously?"
    Anthony: "Told you so. Lots of companies are doing it nowadays."

    -> "I mean, only if you're stuck, right?"
        Nadia: "I guess. ChatGPT has got me out of a lot of academic trouble."
        Vera: "My friend once wrote an 8 page essay in 3 hours with it. But I don't think she did very well."
        Eleanor: "But the important part is that we edit it and make it our own work."
    
    -> "I also use it to edit my crossdressing photos."
        <<if getNPCKnows("A")>> 
            <<changeConfidence(-5)>>
            Anthony: "Oh my god, not this gay shit again." (-5% confidence)
        <<else>>
            <<changeRelationship("A", -5)>>
            Anthony: "What?" (-5% Anthony relationship)
        <<endif>>
        You: "Yeah, if I don't have time for makeup before going out, I use FaceApp to edit my face."
        <<if getNPCKnows("V")>>
            Vera: "I prefer the real Charlotte!"
        <<else>>    
            Vera: "Love that for you."
        <<endif>>
        Nadia: "Yeah, I've heard of my friends jokingly doing that, but regularly? Wow."
    
    -> "If it makes life easier, then why not use it?"
        Vera: "Art isn't about finding the most efficient method. That's engineering! Or science. Or maths."
        Anthony: "Well, you know what they say, women can't do maths."
        Vera: "I would become a woman in STEM just to make you eat your words. But I'm enjoying my major too much for that."
        Eleanor: "Enough, guys. Let's just move on."

<<jump 6>>
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 1436,237
__PrivCrochet_tags: 
title: 6
---
Nadia: "Okay, my turn!"
[i]She shows a design of two students drawing each other while smiling.[/i]
Eleanor: "Wah, this one is good. Even if it's just a sketch for now."
Vera: "Yeah, you can always add the details later."
Anthony: "Anime again."
Nadia: "What's wrong with that?"
Anthony: "Nothing, nothing... just that you can only draw one type of art, huh?"
Vera: "What's wrong with having a preference? At least she's got something to be proud of."

-> "Nadia, you need to move on from anime. Get better material."
    <<changeRelationship("N", -5)>>
    Nadia: "And who made you the authority on good art?" (-5% Nadia relationship)
    Anthony: "No, I agree. What happened to trying different forms?"
    Nadia: "I'll try different forms when Eleanor wants us to! I know there's, like, typography, pottery, drip paint and all that - "
    Anthony: "And then you go back to anime. I'm sick of it."
    Eleanor: "We all have our own styles, Anthony. Let's just move on."

-> "I wouldn't be caught dead with that on my back."
    Vera: "That was very cruel."
    Eleanor: "{$name}! Don't be rude."
    <<changeRelationship("A", 5)>>
    Anthony: "Wow, you dared to say what we all don't dare to." (+5% Anthony relationship)
    <<changeRelationship("V", -5)>>
    Vera: "Speak for yourselves. I'm not an anime fan but I'd wear this to support my friend." (-5% Vera relationship)

-> "What's wrong with anime?"
    Anthony: "Don't you think it's very cringey?"
    Vera: "It's a matter of personal taste. Who are you to tell Nadia what she can't and can draw?"
    Anthony: "Fine. I hope you enjoy your influx of sweaty weeaboos who think this is some anime club."
    Eleanor: "Anthony, we don't gatekeep anyone from the club."
    You: "Again, what's wrong with a majority of our club members liking anime?"
    Vera: "Exactly. We're small enough to not have an identity yet. Maybe this is what we need."
    Anthony: "Ugh. I guess if this club starts drawing hentai, I really need to reconsider what I'm doing here."
    Nadia: "You know that's not what I draw!"
    Eleanor: "Enough, all of you. Let's move on."

-> "Weren't you drawing anime waifus before our session started?" <<if $satWithAnthony>>
    [i]Anthony looks away worriedly.[/i]
    Anthony: "It's... not the same!"
    Anthony: "And you shouldn't be looking at other people's drawings anyway!"

    -> "You literally acknowledged it to me."
        <<changeRelationship("A", -5)>>
        Anthony doesn't have anything to say. (-5% Anthony relationship)
        <<changeRelationship("N", 5)>>
        You: "Sounds like you're just insecure in your preferences. But can you not give Nadia shit over hers?" (+5% Nadia relationship)
        Nadia: "Anthony, I didn't know you liked anime too."
        Vera: "There's nothing wrong with that, Anthony. Not everything is cringe." (+ 10 XP)
        Anthony: "Let's... just move on."
    
    -> "Okay, sorry..."
        <<changeConfidence(-5)>>
        Anthony: "Keep your nose out of other people's business." (-5% confidence)
        [i]Vera rolls her eyes.[/i]

-> "It's Nadia's work. Respect it as you would expect us to respect yours." #skillcheck #cha #8
    <<set $diceroll = diceRoll($cha)>>
    [You rolled a {$diceroll}.]
    <<if $diceroll >= 8>>
        <<changeRelationship("V", 5)>>
        [i]Anthony is thrown off by the strength in your statement, and keeps quiet.[/i]
        [i]Vera nods in approval.[/i] (+5% Vera relationship) 
        <<addExperience(10)>>
        Vera: "Yeah, think about that. We've given you space to show your work. Is that too much to ask for from you?" (+ 10 XP)
        Eleanor: "Okay, okay, moving on... Vera, your turn."
    <<else>>
        Anthony: "Oh wow, so we're not allowed to have our own preferences."
        You: "That is not what I said."
        Anthony: "Well then good luck with those who don't like anime. I'm sure I'm not the only one."
        Vera: "Are we done here? Can we move on, Eleanor?"
    <<endif>>

<<jump 7>>
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 1680,240
__PrivCrochet_tags: 
title: 7
---
[i]Vera turns her paper around. It's a butterfly with rainbow coloured wings.[/i]
Nadia: "Rainbows again? That's becoming your trademark already!"
Vera: "Yep. Rainbows are colourful! They can mean many things, like diversity, creativity, acceptance..."
[i]Anthony gives an exasperated sigh.[/i]
Eleanor: "What's wrong, Anthony?"
Anthony: "Nothing. Just... I can't imagine anyone would want to wear this shirt."
Vera: "Why?"
Anthony: "Don't you think it's just a little loud? And there's no indication that we're the Art Club at all."
Nadia: "I guess so. I mean, maybe people will think we're some butterfly club."

-> "Yeah, what's with that butterfly, anyway?"
    [i]Vera blushes a bit.[/i]
    Vera: "I think it represents transformation. You know, from caterpillar to butterfly."
    Vera: "I've always been intrigued by that possibility."
    Anthony: "Oh my god, not this gender nonsense again."
    -> "I agree. Keep gender out of our club."
        <<changeConfidence(-5)>>
        Vera: "It was never supposed to be only about gender. But I guess we see what we want to see, right?" (-5% confidence)
        Anthony: "I see a bad design." 
        Vera: "I'd pray for you if I still believed. Hope you can find yourself or whatever they say."
    -> "It's not about gender." #skillcheck #int #8
        Anthony: "Oh yeah? What's it about then?"
        <<set $diceroll = diceRoll($cha)>>
        [You rolled a {$diceroll}.]
        <<if $diceroll < 8>>
            You: "It's for everybody."
            Anthony: "Sounds like some woke nonsense to me. Stuff that shouldn't be on a logo."
            [i]There's no convincing him, it seems.[/i]
        <<else>>
            You: "It can be for anyone. I'm sure you've felt the same."
            You: "Haven't you felt out of place? Or wanted a place where you could be yourself?"
            You: "That's what the butterfly means. We transform from a caterpillar into something beautiful."
            <<changeRelationship("V", 5)>>
            Vera: "Couldn't have said it better myself." (+5% Vera relationship)
            Nadia: "I'm convinced."
            Anthony: "Fine, whatever..."
        <<endif>>
    -> (let's just move on...)
        [i]You don't really feel like escalating this.[/i]

-> "I'm not a fan of the logo, but I appreciate Vera's efforts." #skillcheck #emp #8
    [i]It's Vera's turn to sigh.[/i]
    <<changeRelationship("V", 5)>>
    Vera: "Thanks, {$name}."
    Anthony: "Then she should be okay with us not accepting it."
    Vera: "Well, yeah, it was just a suggestion."
    Vera: "Still, I feel like there's some... other reason you don't like my logo, right, Anthony?"
    Anthony: "Don't put words into my mouth."
    <<set $diceroll = diceRoll($emp)>>
    [You rolled a {$diceroll}.]
    <<if $diceroll < 8>>
        [i]You're taken aback, and so is Vera.[/i]
        Vera: "I never did that - "
        Eleanor: "Please, guys, let's move on."
    <<else>>
        You: "Anthony, if you've got something to say, at least be honest about it."
        Anthony: "I... uh..."
        You: "Vera will be okay if her design isn't accepted, but it seems you're still aggravated at something?"
        Anthony: "You know what, fine, I don't care."
        <<changeRelationship("V", 5)>>
        Vera: "Thanks, that was better than a fight." (+5% Vera relationship)
    <<endif>>

-> "Isn't there something else that can symbolise inclusivity?" #skillcheck #int #8
    Anthony: "Like what?"
    [i]You think for a moment.[/i]
    <<set $diceroll = diceRoll($int)>>
    [You rolled a {$diceroll}.]
    <<if $diceroll < 8>>
        You: "Uh... Sorry. I'm not sure."
        Anthony: "Then we shouldn't make that the focus of the logo."
    <<else>>
        You: "An umbrella."
        Anthony: "What?"
        Vera: "Why?"
        You: "You know... an umbrella can bring everyone together under it!"
        You: "And if you colour the umbrella rainbow, it'll be less obvious than an outright rainbow."
        <<addExperience(10)>>
        You: "Win-win?" (+ 10 XP)
        Eleanor: "Hmm... we'd have to explain why our logo is an umbrella, though."
        Eleanor: "But it could just be one part of the design. Thanks, {$name}!"

-> "I think her logo is good. Yours could never compare."
    Anthony: "Then, as a guy, tell me, are you willing to wear this shirt?"
    -> "Yes, I'd be proud to wear it."
        [i]Anthony directs his glare away from you.[/i]
        Anthony: "Siao lang. Of course you would wear a rainbow."
        <<changeConfidence(5)>>
        You: "Sounds like someone just can't appreciate art." (+5% confidence)
    -> "Why, scared ah?"
        Anthony: "Where got... I just think it's bad. Not worth printing."
        You: "Then you shouldn't have brought being a guy up, right?"
        <<addExperience(10)>>
        You: "If it was as bad as you say, no one would want to wear this design. But clearly you've got something else on your mind." (+10 XP)
        [i]Anthony folds his arms and looks away.[/i]
    -> "No..."
        Anthony: "Told you. No normal guy would wear this."
        Anthony: "But you? You might as well stop pretending and just wear it."

-> "Yeah, the logo is inappropriate."
    Anthony: "Exactly, our other members don't like it."
    Vera: "Speak for yourself. I like it."
    Anthony: "Your word against mine. And the others'."
    Vera: "And, 'inappropriate'? Don't tell me rainbows make us cry now?"
<<jump 8>>
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 1917,233
__PrivCrochet_tags: 
title: 8
---
Eleanor: "Okay, it seems like we haven't really come to a consensus here..."
Eleanor: "Which is okay. Maybe we just need a bit more time to decide."
Anthony: "Wait. Before we move on, there's something I want to ask Vera."
Vera: "Shoot."
Anthony: "Your logo talks about including everybody. But what about the straights?"
Vera: "What?"
Anthony: "Pride means gay, right? But where is the straight representation?"
Vera: "You can't possibly be serious right now."

-> "Rainbows don't just mean LGBTQ+. Didn't you listen to what she said?"
    Vera: "Thanks, {$name}. At least you get it."
    Anthony: "I still can't shake the association. I don't think our future members will either."
    -> "You know what else is rainbow coloured? Gaming PCs." #skillcheck #int #8
        <<set $diceroll = diceRoll($int)>>
        [You rolled a {$diceroll}.]
        <<if $diceroll >= 8>>
            You: "Do you game, Anthony?"
            Anthony: "Yeah, sometimes..."
            You: "Think about it. If heterosexual men really hated rainbows, would they really buy a PC with lots of RGB lighting?"
            Anthony: "I guess not..."
            You: "But they do anyway. And it's not LGBTQ+ at all, not for them. They just wanna play games."
            Anthony: "What's your point?"
            <<addExperience(10)>>
            You: "You just wanna draw. The concept is similar." (+ 10 XP)
            Vera: "The fact that you're wearing a rainbow while doing it shouldn't make a difference."
        <<else>>
            Anthony: "That doesn't matter. Rainbows are political now."
            Anthony: "Name me one rainbow product that doesn't remind people of LGBT nonsense."
            [i]You don't know how to respond to that.[/i]
        <<endif>>
    -> "I guess we'll have to agree to disagree."
        Anthony: "Then I guess I won't ever wear the shirt in public. Which defeats the purpose of making one."
        <<changeConfidence(-5)>>
        Anthony: "Eleanor, we should have spent time doing something better." (-5% confidence)
    -> "That's a you problem."
        Anthony: "Then I guess being a gay is also a Vera problem."
        Vera: "I don't see it as a problem. Only you do."
        Eleanor: "Enough! Guys."

-> "The straight representation is inbuilt."
    Anthony: "What?"
    You: "Isn't the pride flag about diversity, about accepting everybody?"
    Vera: "Yes, that includes our straight allies."
    Anthony: "Then why don't I see any straight people wearing rainbows?"
    Nadia: "I will do it!"
    Eleanor: "And you can do it too, Anthony. Nothing's stopping you from it."
    Vera: "Is your masculinity so fragile that a rainbow will shatter it to pieces?"
    -> "Good one, Vera."
        Anthony: "Ugh. This club isn't the right place for me."
        Vera: "Honestly? You've been rude and judgemental to multiple members of the club." 
        Vera: "It's classic douchebag alpha male behaviour, and I'm not surprised you'd be so opposed to the rainbow." 
        Anthony: "I'm just saying my opinion. If you can't handle it, that says more about you than about me."
        Vera: "I'm surprised Eleanor isn't doing anything about you."
        Nadia: "Guys, please chill..."
        Vera: "Nadia, didn't you feel insulted when he mocked your drawing style?"
        [i]Nadia falls silent.[/i]
        Anthony: "Fine. I know where I'm not wanted."

        -> "She didn't mean that, Anthony."
            Anthony: "Oh, of course. You praise her and then turn around to say she didn't mean that."
            Eleanor: "We don't want to see a club member go. Especially so early in the term - "
            Anthony: "So this is just about filling seats?"
            -> "Fine. Anthony. We're sorry." #skillcheck #emp #8
                <<set $diceroll = diceRoll($emp)>>
                [You rolled a {$diceroll}.]
                <<if $diceroll < 8>>
                    Vera: "Sorry for what? For allowing bigotry to run free here?"
                    Eleanor: "That's not what we meant, both of you!"
                    Nadia: "Guys, please... you're fighting for no reason..."
                    <<changeRelationship("A", -5)>>
                    Anthony: "No, this ends here. I'm just here to draw. That's all." (-5% Anthony relationship)
                <<else>>
                    Vera: "What!?"
                    Anthony: "For once, I agree."
                    You: "Hear me out. I know LGBTQ+ stuff is complex."
                    Anthony: "Then you know it's all just made up."
                    You: "I know that it's something everyone struggles with at some point. Including you."
                    Anthony: "I'm not gay!"
                    You: "I never said you were. In fact, I want to ask, have you ever met a gay person before?"
                    Anthony: "I've seen them on Youtube - "
                    You: "No count. I meant, real life."
                    [i]Anthony shakes his head.[/i]
                    You: "Then I know it's difficult to understand at first. And your first time getting to know a queer person, you got scolded."
                    You: "That must feel bad. So on behalf of Vera, I'd say sorry."
                    Vera: "I don't need you to apologise for me."
                    -> "Of course. I want you to apologise to him." #skillcheck #int #8
                        <<set $diceroll = diceRoll($int)>>
                        [You rolled a {$diceroll}.]
                        <<if $diceroll >= 8>>
                            Vera: "You've made my jaw drop twice in one minute."
                            You: "Go ahead, pick it up. You've got something to say."
                            Vera: "Fine. Anthony, I am sorry I got mad. Can we be more polite to each other now?"
                            [i]Anthony looks at the rest of the club, their eyes squarely on him.[/i]
                            <<addExperience(10)>>
                            Anthony: "Fine... Sorry also lor." (+ 10 XP)
                        <<else>>
                            Vera: "For what? He's the one causing all this trouble. What did [i]I[/i] do?"
                            [i]You're caught off guard by Vera's statement. The silence is deafening.[/i]
                        <<endif>>
                    -> (say nothing)
                        [i]You're caught off guard by Vera's statement. The silence is deafening.[/i]
            -> (say nothing)
                Eleanor: "You know that's not what we meant, Anthony."
                <<changeRelationship("A", -5)>>
                Anthony: "I know that I'm not welcome here, and you weren't a strong enough leader to keep Vera in check." (-5% Anthony relationship)
        -> "Good riddance."
            Eleanor: "Anthony, please."
            Vera: "Eleanor, don't tell me you're really gonna kiss up to him."
            Anthony: "The writing's on the wall, Eleanor. It's been fun, but as they say..."
            He looks right at you.
            <<changeRelationship("A", -10)>>
            Anthony: "Good riddance." (-10% Anthony relationship)
    -> "Uh, Vera, that was kind of rude..." #skillcheck #emp #8
        You: "I know you're pissed off at Anthony. But we have to be the bigger person here."
        <<set $diceroll = diceRoll($emp)>>
        [You rolled a {$diceroll}.]
        <<if $diceroll >= 8>>
            You: "Your feelings are valid. But we cannot stoop to his level."
            Nadia: "Honestly... I understand where he's coming from..."
            Vera: "You shouldn't just grin and bear it, Nadia. If I were you, I'd be a lot angrier."
            Nadia: "Well, yes. But I don't want to lose a member of the club over this."
            Eleanor: "Anger won't solve the problem, Vera. Your concerns are valid, but we don't want a full blown fight here."
            Vera: "Ignoring the fact that you're all cishet people trying to tell a queer person how to be offended, fine. I'll stand down."
            Nadia: "We're not invalidating your feelings, Vera. We just want peace and good vibes."
            Vera: "Then you should really question how we get there."
            <<addExperience(10)>>
            You: "Not by tearing down other people. Keep it civil." (+10 XP)
        <<else>>
            Vera: "He refuses to understand what I'm saying. He taunted Nadia's art, and didn't produce any of his own."
            <<changeRelationship("V",-5)>>
            Vera: "You're telling me you want to tolerate that behaviour here just to be polite?" (-5% Vera relationship)
        <<endif>>

    -> (say nothing)
        [i]Vera, you never fail to impress.[/i] 
        [i]But maybe we shouldn't poke the anthill any more today?[/i]

-> "That was a valid question, Vera."
    [i]Vera sighs in exasperation.[/i]
    Vera: "Fine. Since you guys don't seem to get it, the rainbow is meant to represent our straight allies too."
    Vera: "So it's not like pride flags exclude straight people."
    Vera: "But why is it that you want to intrude in our spaces, at our expense?"
    -> "No, it's why you want to keep pushing your agenda onto us."
        <<changeRelationship("A", 5)>>
        Anthony: "Exactly! Finally, someone gets it." (+5% Anthony relationship)
        <<changeRelationship("V", -5)>>
        Vera: "I can't believe you'd say that." (-5% Vera relationship)
        Vera: "If you don't like to see it, don't view it, simple."
        Anthony: "Is this the part where you complain about not having enough 'representation'?"
        Vera: "I'm okay with us not using my logo. But still, I want this club to be open to all."
        Anthony: "Okay la okay la, any one of your 76 genders is welcome here, happy?"
        Vera: "I know that's not what you really think."
        Anthony: "Attack helicopter also can."
        Vera: "We're really going with that joke, huh?"
        Anthony: "Until you stop trying to make our club shirt a joke, sure."
    
    -> "We're not trying to do that."
        Vera: "Then what do you want? Cishet people are so complex sometimes."
        You: "I mean... weren't you ever confused about who [i]you[/i] were?"
        Vera: "Of course."
        Nadia: "Then you should know that others are confused too. We don't mean harm. Straight people aren't the enemy."
        <<addExperience(10)>>
        You: "Yeah, I think we all need a bit more empathy towards one another instead of just fighting..." (+10 XP)
        [i]Vera smirks.[/i]
        Vera: "That's such a straight person answer. But fine. Whatever. As long as you accept me."
        Anthony: "No one was rejecting you. You just gaslit yourself into thinking that."
        Vera: "That in itself is gaslighting, but I'm not feeding the trolls."
    
    -> "Wait, weren't we talking about the logo?"
        Eleanor: "Yes, thanks, let's get back on track."
        Eleanor: "I think we should compromise on this. The rainbow can still exist, but as a background element."
        Vera: "This isn't over, Eleanor."
        Eleanor: "Please. Take it outside. If we continue here, I might as well hold a sleepover in this classroom."
        Nadia: "Not a bad idea."
        You: "Wait, which one? The sleepover or continuing outside?"
        <<changeConfidence(5)>>
        Nadia: "Yes." (+5% confidence)
        The joke earns a smirk from both Vera and Anthony, but not much else.

-> "Yeah Vera, don't you think this is a little too political?"
    <<jump 8_why>>
    
-> "Vera, hold it together..."
    Vera: "Sorry. It's just... so hard to explain this sometimes, you know?"
    [i]You realise the burden is now on you to explain.[/i]

    -> "Anthony, no one's forcing you to wear this." #skillcheck #cha #8
        <<set $diceroll = diceRoll($cha)>>
        [You rolled a {$diceroll}.]
        <<if $diceroll >= 8>>        
            Anthony: "But it's the club shirt. You think all of our members are gonna like the gay shit too?"
            You: "It's not just LGBTQ+. You gotta own the shirt. Make the design your own."
            You: "Sure, if you don't like rainbows, that's fine. We can change that."
            You: "But we gotta respect the people who put in effort to submit their stuff, right?"
            Anthony: "Not if it's trash."
            <<addExperience(10)>>
            You: "You're standing on very shaky ground there, Mr logo finder dot com." (+ 10 XP)
            Anthony looks away. He can't meet your glance.
            You: "All we're asking for is mutual respect, yeah? You don't like, you say it nicely. They don't like, they also say nicely. Okay?"
            You've managed to explain this simply enough for Anthony to begrudgingly agree.
            Anthony: "Fine. I'm sorry Vera, and Nadia."
            Nadia: "It's okay."
            Vera: "I'll let this go." 
        <<else>>
            You: "Just calm down..."
            Vera: "You're telling me to be calm about this idiot?"
            Anthony: "Look at the SJW flipping out about the stupidest shit."
            Eleanor: "Guys, stop!"
            <<changeConfidence(-5)>>
            [i]No one wants to hear that they need to calm down, especially when they're not calm.[/i] (-5% confidence)
        <<endif>>
    -> "Anthony, we're sorry." #skillcheck #emp #8
        <<set $diceroll = diceRoll($emp)>>
        [You rolled a {$diceroll}.]
        <<if $diceroll >= 8>>
            Vera: "What!?"
            Anthony: "For once, I agree."
            You: "Both of you have been fighting a lot. Someone had to take the first step to apologise."
            Anthony: "What's the use if it's an empty apology?"
            You: "I understand how you feel."
            You: "You're just a dude trying to make sense of the world, and enjoy it through art. We aren't so different."
            Anthony: "I'm nothing like you."
            You: "Are we? I'm new to our new friends here too, as new as you are."
            Anthony: "You can call them friends. I wouldn't."
            You: "If you'd taken the effort to know them, you could have." 
            Nadia: "It's not too late to make friends; isn't that what you're here for?"
            Anthony looks away, but you can see the doubt on his face.
            You: "Don't you think you should return the favour?"
            Anthony: "Fine. Sorry, Vera."
            Vera: "Sorry, Anthony."
            <<addExperience(10)>>
            Eleanor: "That's a start." (+ 10 XP)
        <<else>>
            You: "Just calm down..."
            Vera: "You're telling me to be calm about this idiot?"
            Anthony: "Look at the SJW flipping out about the stupidest shit."
            Eleanor: "Guys, stop!"
            <<changeConfidence(-5)>>
            [i]No one wants to hear that they need to calm down, especially when they're not calm.[/i] (-5% confidence)
        <<endif>>
    -> "Actually, I think Anthony has some valid points."
        <<jump 8_why>>
    -> "Just calm down..."
        Vera: "You're telling me to be calm about this idiot?"
        Anthony: "Look at the SJW flipping out about the stupidest shit."
        Eleanor: "Guys, stop!"
        <<changeConfidence(-5)>>
        [i]No one wants to hear that they need to calm down, especially when they're not calm.[/i] (-5% confidence)
<<jump 9>>
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 1920,480
__PrivCrochet_tags: 
title: 8_why
---
[i]Vera folds her arms.[/i]
Vera: "Why?"
-> "It's like those companies that only change their logo to rainbow in June and then change it back in July."
    Vera: "Yeah, but for us, it's our permanent logo design."
    You: "So it's political all months of the year. Which is worse. LGBTQ+ was never our identity."
    Vera: "I never wanted it to be. This isn't Gay Arts Club, though that would certainly be interesting."
-> "Kinda seems like this is a design for [i]you[/i], not the club."
    [i]Vera is too flustered to answer.[/i]
    Anthony: "Heh."
    You: "Like, we get it, you're gay, but can you stop pushing it so openly?"
    <<changeRelationship("A", 5)>>
    Anthony: "Yeah, we're tired of seeing it so often." (+5% Anthony relationship)
    <<changeRelationship("V", -5)>>
    Vera: "What do you even mean?" (-5% Vera relationship)
    <<changeConfidence(-5)>>
    Vera: "I thought you'd be better than this." (-5% confidence)
Vera: "You seem to be overlooking the point that rainbows can also mean diversity and creativity. The stuff artists aspire to."
You: "Yeah, but what if people are uncomfortable with the theme?"
Vera: "Art is meant to disturb the comfortable and comfort the disturbed."
Anthony: "And you're gonna disturb a lot of comfortable people who want to join our club."
Vera: "That's just your personal biases getting in the way."

<<jump 9>>
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 2160,240
__PrivCrochet_tags: 
title: 9
---
Eleanor: "Well... thanks for the discussions, everyone."
Eleanor: "I know we've all been through a lot today, so please go home and don't think too much about it, okay?"
Eleanor: "You can always text me if you want to talk about anything."
// (from here, the NPCs can still be talked to, only if you have a Neutral/Good relationship with them)
<<goto_room_cca_2_end()>>
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 2400,240
__PrivCrochet_tags: 
title: 10
---
Vera: "I'm honestly still so angry at Anthony."
Vera: "What do you think we should do about him?"

-> "Complain to Eleanor."
    Vera: "I don't think she can do anything. Poor girl. Her hands are tied."
    -> "Why?"
        Vera: "As much as I hate to say it, I can understand where she's coming from."
        Vera: "If she kicks people out, our club becomes smaller. And our reputation might tank."
        Vera: "And, if you've ever tried to do that before, it's not as easy as it looks."

    -> "We have to go beyond Eleanor for this."
        You: "What will you do?"
        Vera: "I mean, I'm not so hateful that I would actively ask him to get lost."
        Vera: "But there are other ways to achieve that goal. We'll see what we can do."

-> "Vera. You have to let this go."
    Vera: "Oh sure, let me just forget about the guy who's been making life so tough for us here. So simple!"
    -> "Have you ever wanted to throw a hot coal at someone before?" #skillcheck #emp #8
        Vera: "That's oddly specific."
        You: "Well I haven't either. But the fact that you're holding it, means you're the one getting burned."
        Vera: "Then I'd just throw it already."
        You: "Doesn't change the fact that you got burned by it."
        Vera: "True..."
        You: "The only winning move is not to hold the hot coal in the first place."
        You: "And that means you don't get your revenge, but is it really worth it if it hurts you?"
        Vera stays silent for a moment.
        <<set $diceroll = diceRoll($emp)>>
        [You rolled a {$diceroll}.]
        <<if $diceroll >= 8>>
            Vera: "Fine. I'll give that a shot."
            Vera: "Surely that can't apply to all things, though."
            You: "I don't think it does. But in this case, I think you can afford to let the coal down." (+ 10 XP)
        <<else>>
            Vera: "Nope. You still don't understand what I'm going through."
            Vera: "But you're not the target of my anger. Let's just leave."
        <<endif>>
    -> "I'm sorry."
        Vera: "These things aren't as easy to let go if you're caught in the middle of them."
        Vera: "If he'd insulted me for being trans, I'd have left instead."
        You: "Was he going to do that?"
        Vera: "He was already picking up on my height and voice. I don't need more dysphoria."
        You: "Yeah, I guess it's best to keep our distance."

-> "We have no place for bigotry here."
    Vera: "I wish this wasn't so hard."
    Vera: "I always thought uni was a safe space, but people like him still crawl out from under a rock to spew their poison."
    -> "Don't lose hope. The world is becoming more accepting."
        Vera: "It's so hard to tell who's accepting and who's not."
        <<if getNPCKnows("V")>>
            Vera: "I mean, you probably face a lot of shit for being Charlotte too."
            You: "Yeah. I'll have to make a decision on her soon."
            Vera: "Good luck with that."
        <<endif>>
    -> "Don't trust people like him from now on."
        Vera: "It's easy to say men are trash. Unfortunately, we must continue to interact with them."
        Vera: "But you know, if this whole school, this whole country, is gonna be so hostile..."
        Vera: "Maybe it's not worth staying here long-term."
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 2399,479
__PrivCrochet_tags: 
title: 11
---
Nadia: "Oh my goodness, what just happened?"
Nadia: "We nearly lost a club member there!"
-> "Relax, he's on his way out anyway."
    Nadia: "Well, we're still down to only 3 people. Excluding Eleanor."
    -> "I know how you feel, but we'll get over this."
        <<jump 11_knowHow>>

    -> "That's her problem, not ours."
        Nadia: "How can you say that? We should help her to solve this too."
        Nadia: "Not just because we're her club members, but because we're her friends too."
        Nadia: "And Anthony could have been a friend too. But look what happened."

    -> "You're telling me you seriously want a guy who insulted everyone in the club to stay?"
        Nadia is silent for a moment.
        Nadia: "Well, if you put it that way... I guess not, but still..."
        -> "You've got a good heart, Nadia."
            You: "But you also need to draw boundaries and recognise who's being problematic."
            Nadia: "Thanks... let's hope any future members we get aren't as toxic as he is."
        
        -> "Told you so."
            [i]Nadia sighs and turns away.[/i]

-> "I think it's a damn shame if he leaves."
    Nadia: "I wouldn't call it that, but..."
    You: "Vera is to blame. Look at how she was aggravating him."
    Nadia: "I'm not sure about that either, didn't he start it?"
    You: "He was perfectly in the right to express his opinion. That's a level of assertiveness we should aspire to."
    You: "If not, who's to say we won't get cancelled for the smallest thing next?"
    Nadia: "I guess..."
    You can tell she's not entirely convinced, but this is about as far as it goes.

-> "I know how you feel, but we'll get over this."
    <<jump 11_knowHow>>
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 2640,240
__PrivCrochet_tags: 
title: 12
---
Anthony: "That bitch, Vera."
Anthony: "I'm so close to telling the whole world about her dirty little secret."
You: "Wait, what's that?"
Anthony: "Haven't you seen? She is actually he. How can a woman be taller than me, and have such a deep voice?"
-> "Yes, you should spread the word. The people must know."
    Anthony: "I'll spread it among my friends. We're all losers, but we can all laugh at him."
-> "What the fuck, Anthony?"
    Anthony: "What? Don't tell me you also cannot handle the truth."
    You: "No, this just isn't right."
    Anthony: "Sure, tell me why."
    -> "Don't people have the right to secrets anymore?"
        Anthony: "So you're telling me he's not proud to be an ah gua?"
        You: "If she doesn't want to bring attention to it, that is her right."
        Anthony: "So he's ashamed of what he does."
        You: "You know what? You're impossible to deal with. I'm done here."
    -> "Why don't you bring that up to her face, huh? See how she reacts." #skillcheck #cha #8
        <<set $diceroll = diceRoll($cha)>>
        [You rolled a {$diceroll}.]
        <<if $diceroll >= 8>>
            [i]Anthony stays silent.[/i]
            You: "You don't dare to."
            Anthony: "It's not worth my time. Maybe he will start crying."
            You: "You're just making excuses. All you dare to do is talk shit behind her back."
            <<changeRelationship("A", -5)>>
            [i]Anthony walks off.[/i] (-5% Anthony relationship)
            <<changeConfidence(5)>>
            [i]Well done. You've exposed him for the coward he is.[/i] (+5% confidence)
        <<else>>
            Anthony: "Maybe I will, huh? He would look so pathetic, crying on the floor."
            [i]What did you just hear...[/i]
        <<endif>>
-> "It's just not right!"
    Anthony: "If that's the only thing you can say, you don't have an argument."
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 2655,529
__PrivCrochet_tags: 
title: 11_knowHow
---
Nadia: "I guess so... let's hope that T-shirt design works."
-> "Who knows?"
    Nadia: "I really don't want the club to shut down. It's a great place to make friends."
    You: "Yeah, we can only hope."

-> "Your idea might just save us all."
    Nadia: "What do you mean?"
    You: "Your design. It's cute, it appeals to a lot of people, and we can all tell you put effort into it."
    Nadia smiles a bit.
    <<changeRelationship("N", 5)>>
    Nadia: "Thanks, {$name}. Glad to see you really appreciate my art style." (+5% Nadia relationship)
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 2883,393
__PrivCrochet_tags: 
title: exit
---
Well, that was certainly a furious argument. 
<<addExperience(30)>>
You feel... different, having gone through it all. (+30 XP)
<<goto_room_bedroom()>>
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 240,-40
__PrivCrochet_tags: 
title: anthony_pre
---
<<set $satWithAnthony = true>>
    // Sit next to Anthony
    Anthony: "Hi."
    You: "Hey Anthony. Drawing already?"
    Anthony: "Might as well, right?"
    You: "Well you're a dedicated one."
    Anthony: "If I'm bitchless in real life, who can judge me for wanting a waifu? 2D over 3D any day."
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 480,-40
__PrivCrochet_tags: 
title: vera_pre
---
Vera: "Hey, {$name}."
You: "Hey, Vera! What's up?"
Vera: "Same stuff. Just scrolling through Pinterest. And Instagram. And TikTok."
-> "For art ideas?"
    Vera: "Haha, I'd be a much better student if I was that dedicated, right?"
    Vera: "Only about 1% of what I'm seeing is vibes. The rest of it is fashion and trans politics."
-> "Wow, that's a lot of social media."
    Vera: "Haha, I'd be a much better student if I could get rid of a few of those, right?"
    Vera: "I think it still helps me, though."
    You: "How so?"
    Vera: "I learn from trans content, at least."
===
__PrivCrochet_colorID: 0
__PrivCrochet_position: 240,240
__PrivCrochet_tags: 
title: chair
---
Do you wish to sit? (Will start the session)

-> No (go back)

-> Yes (continue...) #continue 
    <<goto_room_cca_2_seated()>>
//spawn in Nadia and Eleanor
===
